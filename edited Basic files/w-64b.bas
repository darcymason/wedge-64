100 ;
110 ; W-64b - FILE#2
120 ;
130 FINI JMP READY
140 DUMP LDA VARSTART:LDX VARSTART+1
150 STA VARPTR:STX VARPTR+1:JMP GETVAR
160 ;
170 NEXTVAR JSR STOPSHFT:BEQ FINI:JSR CGRET
180 LDA #7:CLC:ADC VARPTR:STA VARPTR
190 LDA VARPTR+1:ADC #0:STA VARPTR+1
200 ;
210 GETVAR JSR SPACE:LDA VARPTR+1
220 CMP STARRAY+1:BCC DUMPOK
230 BNE FINI
240 LDA VARPTR
250 CMP STARRAY:BCS FINI
260 ;
270 DUMPOK LDY #0:LDA (VARPTR),Y
280 BPL NOTINT
290 JMP INTEGER
300 NOTINT JSR PRINT
310 JSR INCPTR
320 CMP #$80
330 BCS STRING
340 FLOATING JSR PRINT:JSR EQUALS:LDA #"{LEFT}":JSR PRINT
350 JSR INCPTR
360 TYA:CLC:ADC VARPTR:STA TEMPX:LDA VARPTR+1:ADC #0:STA TEMPY
370 LDA TEMPX:LDY TEMPY:JSR LOADFAC1
380 JSR CONVFAC1
390 JSR STROUT
400 JMP NEXTVAR
410 ;
420 STRING AND #$7F:JSR PRINT:LDA #"$":JSR PRINT
430 JSR EQUALS:LDA #34:JSR PRINT
440 JSR INCPTR:STA STRLEN
450 JSR INCPTR:STA STRPTR
460 JSR INCPTR:STA STRPTR+1
470 LDY STRLEN:BEQ QUOTE
480 LDY #0:PRLOOP LDA (STRPTR),Y
490 JSR PRINT:INY:CPY STRLEN:BNE PRLOOP
500 QUOTE LDA #34:JSR PRINT
510 JMP NEXTVAR
520 ;
530 INTEGER AND #$7F:JSR PRINT
540 JSR INCPTR:AND #$7F:JSR PRINT
550 LDA #"%":JSR PRINT:JSR EQUALS
560 JSR INCPTR:STA TEMPA ; GET HI BYTE
570 ; HAS HIGH BIT SET IF NEG'VE
580 BPL PLUS
590 LDA #"-":JSR PRINT
600 LDA TEMPA:EOR #$FF:CLC:ADC #0
610 PHA:JSR INCPTR:EOR #$FF:TAX:INX:BNE PULL:PLA:CLC:ADC #1:JMP PRINTIT
620 PULL PLA:JMP PRINTIT
630 PLUS LDA TEMPA:AND #$7F
640 PHA:JSR INCPTR
650 TAX
660 PLA
670 PRINTIT JSR PRINTLN
680 JMP NEXTVAR
690 ;
700 INCPTR INY:LDA (VARPTR),Y
710 RTS
720 EQUALS JSR SPACE:LDA #"=":JSR PRINT:JMP SPACE
730 ;
740 ;  H U N T
750 ;
760 HUNT JSR CRUNCH:JSR CHRGET:LDX #80:STX FILELEN
770 STA DELIM:JSR INPFILE
780 ;
790 BEQ ONLYDL
800 JSR COMMA
810 ONLYDL JSR LNRANGE
820 ;
830 LDY STRPTR:LDX STRPTR+1:STY INTVAL:STX INTVAL+1
840 LDA #4:CLC:ADC NUMCHAR:STA NUMCHAR
850 JSR FINDLN
860 ;
870 NEWLINE JSR STOPSHFT:BEQ ALLGONE
880 LDY #1:LDA (VARPTR),Y  ;HB LINK
890 BEQ ALLGONE
900 ;
910 INY:LDA (VARPTR),Y
920 STA INTVAL          ; GET LN#
930 INY:LDA (VARPTR),Y
940 STA INTVAL+1
950 ;
960 LDA INTVAL+1
970 CMP ENDPTR+1
980 BCC HUNTLOOP     ;CHECK HB
990 BNE ALLGONE
1000 ;
1010 LDA INTVAL
1020 CMP ENDPTR
1030 BEQ HUNTLOOP     ;CHECK LB
1040 BCS ALLGONE
1050 ;
1060 HUNTLOOP LDY #4
1070 KEEPTRY LDA (VARPTR),Y
1080 BEQ NEXTLINE
1090 CMP FILENAME-4,Y
1100 BEQ OKSOFAR
1110 ;
1120 INC VARPTR
1130 BNE OKKKK
1140 INC VARPTR+1
1150 OKKKK JMP HUNTLOOP
1160 ;
1170 OKSOFAR INY   ;GOOD CHR->GET NEXT
1180 CPY NUMCHAR
1190 BNE KEEPTRY
1200 ;
1210 JSR FINDLN:JSR PRLINE   ;BINGO!
1220 ;
1230 ;
1240 NEXTLINE TYA:SEC
1250 ADC VARPTR        ;UPDATE PTR TO
1260 STA VARPTR
1270 BCC NEWLINE       ;NEXT LINE
1280 INC VARPTR+1
1290 JMP NEWLINE
1300 ;
1310 ALLGONE JMP READY
1320 ;
1330 PRLINE LDY #1:JSR SPACE
1340 STY $0F
1350 LDA (VARPTR),Y
1360 BEQ LAB1
1370 JSR $AAD7
1380 INY:LDA (VARPTR),Y
1390 TAX
1400 INY:LDA (VARPTR),Y
1410 STY $49
1420 JSR PRINTLN
1430 LDA #" "
1440 LAB8 LDY $49
1450 AND #$7F
1460 LAB4 JSR $AB47
1470 CMP #$22
1480 BNE LAB2
1490 LDA $0F
1500 EOR #$FF
1510 STA $0F
1520 ;
1530 LAB2 INY
1540 BEQ LAB1
1550 LDA (VARPTR),Y
1560 BNE LAB3
1570 ;
1580 LAB1 RTS        ;ALL DONE!
1590 ;
1600 LAB3 BPL LAB4
1610 CMP #$FF
1620 BEQ LAB4
1630 BIT $0F
1640 BMI LAB4
1650 ;
1660 SEC:SBC #$7F
1670 TAX
1680 STY $49
1690 LDY #$FF
1700 LAB7 DEX
1710 BEQ LAB5
1720 ;
1730 LAB6 INY:LDA $A09E,Y
1740 BPL LAB6
1750 BMI LAB7
1760 ;
1770 LAB5 INY:LDA $A09E,Y
1780 BMI LAB8
1790 JSR $AB47
1800 BNE LAB5
1810 ;
1820 STOPSHFT JSR STOPKEY
1830 BEQ GOBACK
1840 SHIFTON LDA SHIFTKEY
1850 CMP #1
1860 BEQ SHIFTON
1870 GOBACK RTS
1880 ;
1890 ILLEGAL JMP ILLQTY
1900 ;
1910 COL BEQ READYYYY:JSR GETFXD:BNE ILLEGAL
1920 STA 53280
1930 JSR COMMA
1940 JSR GETFXD:BNE ILLEGAL
1950 STA 53281
1960 JSR COMMA
1970 JSR GETFXD:BNE ILLEGAL
1980 STA 646
1990 READYYYY JMP READY
2000 ;
2010 ;
2020 ;
2030 TOOBIG JMP ILLQTY
2040 AUTO JSR GETFXD
2050 BNE TOOBIG
2060 STA INCR:JMP READY
2070 ;
2080 ;
2090 AUTOLN JSR INPFXD:LDY INCR:BEQ GOBACK4
2100 JSR CHRGOT:CMP #0:BEQ GOBACK4
2110 LDA INCR
2120 CLC:ADC INTVAL:TAY:LDA INTVAL+1:ADC #0
2130 JSR FXDFL
2140 LDX #0:KEYBLOOP LDA $0101,X:BEQ KEYBDONE:STA KEYB,X:INX:BNE KEYBLOOP
2150 KEYBDONE INX
2160 LDA #" ":STA KEYB,X
2170 INX:STX KEYBLEN
2180 GOBACK4 JMP FOUNDNUM
2190 ;
2200 DEL BEQ READYY
2210 JSR CRUNCH:JSR CHRGET:JSR LNRANGE
2220 LDA STRPTR:LDX STRPTR+1
2230 STA INTVAL:STX INTVAL+1
2240 JSR FINDLN
2250 LDA VARPTR:LDX VARPTR+1
2260 STA MOVETO:STX MOVETO+1
2270 LDX ENDPTR:LDY ENDPTR+1
2280 INX:BNE STORE:INY:STORE STX INTVAL:STY INTVAL+1
2290 JSR FINDLN:LDA VARPTR:LDX VARPTR+1:STA MOVEFROM:STX MOVEFROM+1
2300 ;
2310 JSR DELMEM
2320 JSR RECHAIN
2330 LDA RECHNPTR:CLC:ADC #2:STA VARSTART
2340 LDA RECHNPTR+1:ADC #0:STA VARSTART+1
2350 JSR NEW:JMP READY
2360 READYY JMP SYNERROR
2370 ;
2380 ;
2390 ;
2400 LNRANGE LDY #$FF
2410 STY ENDPTR+1
2420 INY:STY ENDPTR:STY STRPTR:STY STRPTR+1
2430 JSR CHRGOT:BEQ GOBACK2
2440 BCC PAR1
2450 CMP #$AB:BNE LASTLBL
2460 JSR CHRGET:BCS BADPAR:JMP PAR2
2470 ;
2480 BADPAR JMP SYNERROR
2490 ;
2500 PAR1 JSR INPFXD
2510 LDA INTVAL:LDX INTVAL+1
2520 STA STRPTR:STX STRPTR+1
2530 JSR CHRGOT:CMP #$AB:BNE BADPAR
2540 JSR CHRGET:BEQ GOBACK2
2550 BCC PAR2
2560 GOBACK2 RTS
2570 LASTLBL JMP SYNERROR
2580 ;
2590 PAR2 JSR INPFXD
2600 LDA INTVAL:LDX INTVAL+1
2610 STA ENDPTR:STX ENDPTR+1
2620 JMP CHRGOT
2630 DELMEM LDY #0
2640 DELLOOP LDA MOVEFROM+1:CMP VARSTART+1
2650 BCC STARTDEL
2660 BNE DELDONE
2670 LDA MOVEFROM:CMP VARSTART
2680 BCS DELDONE
2690 ;
2700 STARTDEL LDA (MOVEFROM),Y
2710 STA (MOVETO),Y
2720 INC MOVEFROM:BNE XXX
2730 INC MOVEFROM+1
2740 XXX INC MOVETO:BNE YYY
2750 INC MOVETO+1
2760 YYY BNE DELLOOP
2770 ;
2780 ;
2790 DELDONE RTS
2800 ;
2810 ;
2820 SGETPTR LDA GETPTR:LDX GETPTR+1
2830 STA VARPTR:STX VARPTR+1
2840 RTS
2850 ;
2860 RGETPTR LDA VARPTR:LDX VARPTR+1
2870 STA GETPTR:STX GETPTR+1
2880 RTS
2890 ;
2900 SAVEM JSR SAVEEE:JMP DS
2910 ;
2920 SAVEEE LDX #17:STX FILELEN
2930 JSR INPFILE
2940 SAVEEE2 LDA #1:LDX #8:TAY:JSR SETLFS
2950 ;
2960 JSR CHRGOT:CMP #",":BEQ MLSAVE
2970 LDA #STARTB:LDX VARSTART
2980 LDY VARSTART+1
2990 SAVEIT JSR SAVEPROG:JMP CGRET
3000 ;
3010 ;
3020 MLSAVE JSR INPADD
3030 STA STRPTR:STX STRPTR+1
3040 JSR UPDATE:JSR CHRGET
3050 CMP #",":BNE MLSERR
3060 JSR INPADD
3070 STX TEMPX:TAX:JSR UPDATE:LDY TEMPX
3080 LDA #STRPTR
3090 JMP SAVEIT
3100 ;
3110 MLSERR JMP SYNERROR
3120 ;
3130 ADJUST LDX #0:LDA 646:STA TEMPA
3140 COLLOOP STX TEMPB:LDA COLTBLE,X:JSR PRINT:LDA #<REVERSE:LDY #>REVERSE
3150 JSR STROUT:LDX TEMPB:INX:CPX #16:BNE COLLOOP
3160 LDA TEMPA:STA 646
3170 LDA #13:LDX #5:RETLOOP JSR PRINT:DEX:BNE RETLOOP
3180 JMP READY
3190 ;
3200 UPDATE TYA:CLC:ADC GETPTR:STA GETPTR:LDA GETPTR+1:ADC #0:STA GETPTR+1
3210 RTS
50000 .FILE 8,"w-64c.e8"