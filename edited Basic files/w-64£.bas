100 ; W-64.{POUND} FILE#3
110 ;
120 RENUM JSR CRUNCH:JSR CHRGET:LDA #10:STA RENINC
130 LDA #100:STA RENSTART
140 LDA #0:STA RENSTART+1
150 JSR CHRGOT
160 BEQ RENDEF
170 JSR GETFXD
180 STA RENSTART:STX RENSTART+1
190 ;
200 JSR COMMA
210 JSR GETFXD:BEQ OKINC
220 JMP ILLQTY
230 OKINC STA RENINC
240 ;
250 JSR CHRGOT:BEQ RENDEF
260 JSR COMMA
270 ;
280 RENDEF JSR LNRANGE
290 ;
300 LDA RENSTART:LDX RENSTART+1
310 STA INTVAL:STX INTVAL+1
320 JSR FINDLN
330 LDY #3:LDA (VARPTR),Y:CMP STRPTR+1:BCC RANGEBAD:BNE YOUBET:DEY
340 LDA (VARPTR),Y:CMP STRPTR:BCC RANGEBAD
350 YOUBET LDA STRPTR:LDX STRPTR+1
360 STA INTVAL:STX INTVAL+1
370 JSR FINDLN
380 LDA VARPTR:LDX VARPTR+1
390 STA RENGET:STX RENGET+1
400 ;
410 LDA ENDPTR:LDX ENDPTR+1
420 STA RENENDLN:STX RENENDLN+1
430 STA INTVAL:STX INTVAL+1
440 INC INTVAL:BNE STCOUNT
450 INC INTVAL+1
460 STCOUNT JSR COUNTEM
470 ;
480 JSR MULT
490 LDA RENTEMP+1:CMP #$FA:BCC RANGEOK
500 RANGEBAD LDA #<RENMSGE1:LDY #>RENMSGE1:JSR STROUT:JMP READY
510 ;
520 RANGEOK JSR FINDLN:JSR RGETPTR
530 LDY #1:LDA (GETPTR),Y:BEQ THEMEAT
540 LDY #3:LDA RENTEMP+1:CMP (GETPTR),Y:BCC THEMEAT
550 BNE RANGEBAD
560 LDA RENTEMP:DEY:CMP (GETPTR),Y:BCS RANGEBAD
570 THEMEAT LDA STARTB:LDX STARTB+1
580 STA GETPTR:STX GETPTR+1
590 ;
600 ; PASS1
610 ;
620 RESETQU LDY #0:STY QUMODE
630 LINKLOPP LDY #1:LDA (GETPTR),Y:BNE LINKOK
640 RENDONE1 JMP PASS2
650 ;
660 LINKOK INY:LDA (GETPTR),Y:STA INTVAL:INY:LDA (GETPTR),Y:STA INTVAL+1
670 LDY #4:JSR UPDATE:JSR SGETPTR3:LDY #$FF
680 ALOOP INY:ALOOP2 LDA (GETPTR),Y:BEQ RLNDONE
690 CMP #34:BEQ QUFOUND
700 LDX QUMODE:BNE ALOOP
710 LDX #6:TABLELOP CMP RENTBLE-1,X:BEQ RENBINGO
720 DEX:BNE TABLELOP
730 BEQ ALOOP
740 ;
750 QUFOUND LDA QUMODE:EOR #$FF:STA QUMODE
760 JMP ALOOP
770 ;
780 RLNDONE INY:JSR UPDATE:JMP RESETQU
790 ;
800 RENBINGO PHA:JSR UPDATE:LDY #0:PLA
810 CMP #$CB:BNE RENBING2:JSR CHRGET
820 CMP #$A4:BNE ALOOP2
830 RENBING2 JSR CHRGET:BCS ALOOP2
840 ;
850 JSR SINTVAL:JSR SGETPTR2:JSR CHRGOT:JSR STRFAC:JSR FLFXD
860 ;
870 JSR CKRANGE:BEQ NEEDCHNG
880 JMP AFTERNUM
890 ;
900 ;
910 NEEDCHNG LDA INTVAL:LDX INTVAL+1
920 STA RENENDLN:STX RENENDLN+1
930 JSR COUNTEM:BEQ SOMELAB:LDA #$FF:STA RENCOUNT:STA RENCOUNT+1
940 SOMELAB JSR SPACE
950 ;
960 JSR MULT
970 LDX RENTEMP:LDA RENTEMP+1
980 STX INTVAL:STA INTVAL+1
990 JSR PRINTLN
1000 LDY RENTEMP:LDA RENTEMP+1:JSR FXDFL
1010 ;
1020 JSR RGETPTR3:LDX #$FF
1030 ;
1040 DOITLOOP LDY #0:INX
1050 LDA GETPTR+1:CMP TEMPC+1
1060 BCC NOPROB2:BNE DONETHIS
1070 LDA GETPTR:CMP TEMPC
1080 BCS DONETHIS
1090 NOPROB2 LDA (GETPTR),Y:STA BUFF,X:INY
1100 JSR UPDATE:JMP DOITLOOP
1110 DONETHIS LDY #0:LDA $0100:CMP #"-":BNE BIGLOOP:STA BUFF,X:INX
1120 BIGLOOP LDA $0101,Y:BEQ DONEPT2:STA BUFF,X:INY:INX:BNE BIGLOOP
1130 DONEPT2 STX TEMPH:JSR RGETPTR2:JSR CHRGOT:JSR STRFAC:JSR CHRGOT
1140 LDX TEMPH:LDY #0:BIG2LOOP CMP #0:STA BUFF,X:BEQ DONEPT3
1150 INX:INY:LDA (GETPTR),Y:JMP BIG2LOOP
1160 ;
1170 DONEPT3 INX:INX:STA BUFF,X:INX:INX:INX:STX $0B
1180 JSR RINTVAL:JSR PUTIN
1190 JSR RGETPTR2:JSR STRFAC
1200 LDA STRPTR:LDX STRPTR+1
1210 STA INTVAL:STX INTVAL+1
1220 JSR FINDLN:LDA VARPTR:LDX VARPTR+1
1230 STA RENGET:STX RENGET+1
1240 ;
1250 AFTERNUM JSR RINTVAL
1260 ;
1270 JSR CHRGOT:CMP #",":BEQ GETNEXT1
1280 CMP #$AB:BEQ GETNEXT1
1290 LDY #0:STY QUMODE:JMP ALOOP2
1300 ;
1310 GETNEXT1 JMP RENBING2
1320 ;
1330 ;
1340 ;
1350 GETFXD JSR INPFXD:GETINT LDA INTVAL
1360 LDX INTVAL+1:RTS
1370 ;
1380 ;
1390 MULT LDA RENINC:STA TEMPG:LDA RENSTART:LDX RENSTART+1
1400 STA RENTEMP:STX RENTEMP+1
1410 LDA #0:STA MULTFLAG
1420 ;
1430 LDX #8
1440 MULTLOOP LSR RENINC
1450 BCC DONTADD
1460 LDA MULTFLAG:BNE MULTERR
1470 LDA RENCOUNT
1480 CLC:ADC RENTEMP
1490 STA RENTEMP
1500 LDA RENCOUNT+1
1510 ADC RENTEMP+1
1520 STA RENTEMP+1
1530 ;
1540 DONTADD ASL RENCOUNT
1550 ROL RENCOUNT+1
1560 ROL MULTFLAG
1570 DEX
1580 BNE MULTLOOP
1590 ;
1600 LDA TEMPG:STA RENINC
1610 RTS
1620 MULTERR LDA #$FF
1630 STA RENTEMP:STA RENTEMP+1:LDA TEMPG:STA RENINC
1640 RTS
1650 ;
1660 PASS2 LDA RENGET:LDX RENGET+1
1670 ;
1680 STA GETPTR:STX GETPTR+1
1690 PASS2LP LDY #1:LDA (GETPTR),Y:BEQ PASS2DN
1700 LDY #3:LDA (GETPTR),Y
1710 CMP ENDPTR+1:BCC NOPROB:BNE PASS2DN
1720 DEY:LDA (GETPTR),Y
1730 CMP ENDPTR:BCC NOPROB:BNE PASS2DN
1740 ;
1750 NOPROB LDY #2:LDA RENSTART
1760 STA (GETPTR),Y
1770 INY:LDA RENSTART+1
1780 STA (GETPTR),Y
1790 ;
1800 LDY #0:LDA (GETPTR),Y:TAX
1810 INY:LDA (GETPTR),Y
1820 STA GETPTR+1:STX GETPTR
1830 LDA RENINC
1840 CLC:ADC RENSTART:STA RENSTART
1850 BCC PASS2LP
1860 LDA RENSTART+1:ADC #0:STA RENSTART+1
1870 JMP PASS2LP
1880 ;
1890 PASS2DN JMP READY
1900 ;
1910 ;
1920 SERROR9 JMP SERROR
1930 ;
1940 MEMORY DEC GETPTR:LDA GETPTR:CMP #$FF:BNE NOSUBT
1950 DEC GETPTR+1
1960 NOSUBT JSR INPADD
1970 STA STRPTR:STX STRPTR+1
1980 JSR UPDATE:JSR CHRGET:CMP #",":BNE SERROR9
1990 JSR INPADD
2000 STA ENDPTR:STX ENDPTR+1
2010 ;
2020 JSR CGRET
2030 LLOOPP JSR STOPSHFT:BEQ PASS2DN
2040 LDA STRPTR+1:CMP #>BLOCK:BCC LETEM1
2050 LDA #>BLOCK:CLC:ADC #$10:STA TEMPH:LDY STRPTR+1:CPY TEMPH:BCS LETEM1
2060 ;
2070 JMP READY
2080 ;
2090 LETEM1 LDA #".":JSR PRINT
2100 LDA STRPTR+1:LDX STRPTR
2110 JSR PRINTADD
2120 LDY #0
2130 PRINTMOR JSR SPACE
2140 LDA (STRPTR),Y
2150 STY TEMPH:JSR PRINTBYT:LDY TEMPH
2160 INY:CPY #8:BNE PRINTMOR
2170 JSR SPACE
2180 LDA #"{REVERSE ON}":JSR PRINT
2190 LDY #0
2200 BYTLOOP LDA (STRPTR),Y
2210 LDX #0:STX QUMODE
2220 CMP #161:BCS OOKK
2230 CMP #32:BCC NOTOOKK
2240 CMP #128:BCC OOKK
2250 NOTOOKK LDA #$2E
2260 OOKK JSR PRINT
2270 INY:CPY #8:BNE BYTLOOP
2280 ;
2290 TYA:CLC:ADC STRPTR:STA STRPTR
2300 LDA STRPTR+1:ADC #0:STA STRPTR+1
2310 JSR CGRET
2320 ;
2330 ;
2340 LDA STRPTR+1:CMP ENDPTR+1:BCC GOLLOOPP
2350 BNE NOLLOOPP
2360 ;
2370 LDA STRPTR:CMP ENDPTR:BCC GOLLOOPP
2380 BNE NOLLOOPP
2390 ;
2400 GOLLOOPP JMP LLOOPP
2410 NOLLOOPP JMP READY
2420 ;
2430 ;PUT THE LINE IN
2440 PUTIN JSR $A613    ; FIND BASIC LINE
2450 BCC L1
2460 LDY #1:LDA ($5F),Y
2470 STA $23:LDA $2D:STA $22
2480 LDA $60:STA $25:LDA $5F
2490 DEY:SBC ($5F),Y
2500 CLC:ADC $2D
2510 STA $2D:STA $24
2520 LDA $2E:ADC #$FF
2530 STA $2E:SBC $60
2540 TAX
2550 SEC:LDA $5F
2560 SBC $2D:TAY
2570 BCS L2
2580 INX:DEC $25
2590 L2 CLC:ADC $22
2600 BCC L3
2610 DEC $23:CLC
2620 L3 LDA ($22),Y
2630 STA ($24),Y
2640 INY:BNE L3
2650 INC $23:INC $25
2660 DEX:BNE L3
2670 L1 JSR BACKTEXT:JSR NEW ; UNDER [ NEW ]
2680 JSR RECHAIN
2690 LDA BUFF
2700 BEQ RETRN
2710 CLC:LDA $2D
2720 STA $5A:ADC $0B
2730 STA $58:LDY $2E
2740 STY $5B
2750 BCC L4
2760 INY
2770 L4 STY $59
2780 JSR MOVEMEM ; MOVE MEMORY
2790 LDA INTVAL:LDY INTVAL+1
2800 STA $01FE:STY $01FF
2810 LDA $31:LDY $32  ;END BASIC
2820 STA $2D:STY $2E  ;START BASIC VARS
2830 LDY $0B
2840 DEY
2850 L5 LDA $01FC,Y:STA ($5F),Y
2860 DEY:BPL L5
2870 JSR BACKTEXT:JSR NEW ; UNDER [ NEW ]
2880 JSR RECHAIN
2890 ;
2900 RETRN RTS
2910 ;
2920 NEW LDA $37:LDY $38
2930 STA $33:STY $34
2940 LDA $2D:LDY $2E
2950 STA $2F:STY $30
2960 STA $31:STY $32
2970 JSR $A81D
2980 LDX #$19:STX $16
2990 LDA #0:STA $3E:STA $10
3000 RTS
3010 ;
3020 COUNTEM LDA RENGET:LDX RENGET+1
3030 STA CNTPTR:STX CNTPTR+1
3040 LDY #0:STY RENCOUNT:STY RENCOUNT+1
3050 ;
3060 COUNTLP LDY #1:LDA (CNTPTR),Y
3070 BNE CNTNOVER:LDA #1:RTS
3080 ;
3090 CNTNOVER LDY #3:LDA (CNTPTR),Y
3100 CMP RENENDLN+1
3110 BCC CNTON
3120 BNE CNTDONE
3130 DEY:LDA (CNTPTR),Y
3140 CMP RENENDLN
3150 BCC CNTON
3160 ;
3170 CNTDONE RTS
3180 ;
3190 CNTON LDY #0
3200 LDA (CNTPTR),Y:TAX
3210 INY:LDA (CNTPTR),Y:STA CNTPTR+1:STX CNTPTR
3220 INC RENCOUNT:BNE COUNTLP
3230 INC RENCOUNT+1:BNE COUNTLP
3240 ;
3250 SINTVAL LDA INTVAL:LDX INTVAL+1
3260 STA TEMPA:STX TEMPB
3270 RTS
3280 ;
3290 RINTVAL LDA TEMPA:LDX TEMPB
3300 STA INTVAL:STX INTVAL+1
3310 RTS
3320 ;
3330 SGETPTR2 LDA GETPTR:LDX GETPTR+1
3340 STA TEMPC:STX TEMPC+1
3350 RTS
3360 ;
3370 RGETPTR2 LDA TEMPC:LDX TEMPC+1
3380 STA GETPTR:STX GETPTR+1
3390 RTS
3400 ;
3410 CKRANGE LDA INTVAL+1:CMP STRPTR+1
3420 BEQ ISEQU
3430 BCS CKENDPTR
3440 NOTRNGE LDA #1:RTS
3450 INRANGE LDA #0:RTS
3460 ;
3470 ISEQU LDA INTVAL:CMP STRPTR
3480 BCC NOTRNGE
3490 ;
3500 CKENDPTR LDA INTVAL+1:CMP ENDPTR+1
3510 BCC INRANGE
3520 BNE NOTRNGE
3530 LDA INTVAL:CMP ENDPTR
3540 BCC INRANGE
3550 BEQ INRANGE
3560 BCS NOTRNGE
3570 ;
3580 FXDFL CMP #$80:BCC SKP32768
3590 AND #$7F
3600 JSR SFXDFL:LDA #<CONSTANT:LDY #>CONSTANT
3610 JSR ADDEM
3620 JMP STRIT
3630 ;
3640 SKP32768 JSR SFXDFL
3650 STRIT JSR CONVFAC1
3660 RTS
3670 ;
3680 SGETPTR3 LDA GETPTR:LDX GETPTR+1
3690 STA TEMPI:STX TEMPI+1
3700 RTS
3710 ;
3720 RGETPTR3 LDA TEMPI:LDX TEMPI+1
3730 STA GETPTR:STX GETPTR+1
3740 RTS
3750 ;
3760 BONJOUR LDA #"?":STA DELIM:LDX #17:STX FILELEN
3770 JSR CHRGOT:JSR INPFILE
3780 LDA #<BONJOURM:LDY #>BONJOURM:JSR STROUT
3790 BONGET JSR GET:CMP #"{F8}":BEQ LESAVE
3800 CMP #"N":BNE BONGET:JMP READY
3810 ;
3820 LESAVE LDY #0:LDA SAVETBLE+1:STA TEMPJ:STY SAVETBLE+1
3830 LDA SAVETBLE+2:STA TEMPJ+1:STY SAVETBLE+2
3840 JSR SAVEEE2
3850 LDA TEMPJ:STA SAVETBLE+1
3860 LDA TEMPJ+1:STA SAVETBLE+2
3870 JMP DS
3880 ;
3890 ;
3900 ;
3910 RENTBLE .BYTE $8A,$9B,$CB,$A7,$8D,$89
3920 ;RUN,LIST,GO,THEN,GOSUB,GOTO
3930 RENMSGE1 .ASC " ** ILLEGAL LINE RANGE ** ":.BYTE 13,0
3940 BONJOURM .BYTE 13:.ASC " ? ".BYTE 13,O
3950 CONSTANT .BYTE $90,0,0,0,0
3960 .END 8,"W-64.E5"
3970 TOK64
3980 1.0)