100 ; W-64c FILE#3
110 ;
120 RENUM JSR CRUNCH:JSR CHRGET:LDA #10:STA RENINC
130 LDA #100:STA RENSTART
140 LDA #0:STA RENSTART+1
150 JSR CHRGOT
160 BEQ RENDEF
170 JSR GETFXD
180 STA RENSTART:STX RENSTART+1
190 ;
200 JSR COMMA
210 JSR GETFXD:BEQ OKINC
220 JMP ILLQTY
230 OKINC STA RENINC
240 ;
250 JSR CHRGOT:BEQ RENDEF
260 JSR COMMA
270 ;
280 RENDEF JSR LNRANGE
290 ;
300 LDA RENSTART:LDX RENSTART+1
310 STA INTVAL:STX INTVAL+1
320 JSR FINDLN
330 LDY #3:LDA (VARPTR),Y:CMP STRPTR+1:BCC RANGEBAD:BNE YOUBET:DEY
340 LDA (VARPTR),Y:CMP STRPTR:BCC RANGEBAD
350 YOUBET LDA STRPTR:LDX STRPTR+1
360 STA INTVAL:STX INTVAL+1
370 JSR FINDLN
380 LDA VARPTR:LDX VARPTR+1
390 STA RENGET:STX RENGET+1
400 ;
410 LDA ENDPTR:LDX ENDPTR+1
420 STA RENENDLN:STX RENENDLN+1
430 STA INTVAL:STX INTVAL+1
440 INC INTVAL:BNE STCOUNT
450 INC INTVAL+1
460 STCOUNT JSR COUNTEM
470 ;
480 JSR MULT
490 LDA RENTEMP+1:CMP #$FA:BCC RANGEOK
500 RANGEBAD LDA #<RENMSGE1:LDY #>RENMSGE1:JSR STROUT:JMP READY
510 ;
520 RANGEOK JSR FINDLN:JSR RGETPTR
530 LDY #1:LDA (GETPTR),Y:BEQ THEMEAT
540 LDY #3:LDA RENTEMP+1:CMP (GETPTR),Y:BCC THEMEAT
550 BNE RANGEBAD
560 LDA RENTEMP:DEY:CMP (GETPTR),Y:BCS RANGEBAD
570 THEMEAT LDA STARTB:LDX STARTB+1
580 STA GETPTR:STX GETPTR+1
590 ;
600 ; PASS1
610 ;
620 RESETQU LDY #0:STY QUMODE
630 LINKLOPP LDY #1:LDA (GETPTR),Y:BNE LINKOK
640 RENDONE1 JMP PASS2
650 ;
660 LINKOK INY:LDA (GETPTR),Y:STA INTVAL:INY:LDA (GETPTR),Y:STA INTVAL+1
670 LDY #4:JSR UPDATE:JSR SGETPTR3:LDY #$FF
680 ALOOP INY:ALOOP2 LDA (GETPTR),Y:BEQ RLNDONE
690 CMP #34:BEQ QUFOUND
700 LDX QUMODE:BNE ALOOP
710 LDX #6:TABLELOP CMP RENTBLE-1,X:BEQ RENBINGO
720 DEX:BNE TABLELOP
730 BEQ ALOOP
740 ;
750 QUFOUND LDA QUMODE:EOR #$FF:STA QUMODE
760 JMP ALOOP
770 ;
780 RLNDONE INY:JSR UPDATE:JMP RESETQU
790 ;
800 RENBINGO PHA:JSR UPDATE:LDY #0:PLA
810 CMP #$CB:BNE RENBING2:JSR CHRGET
820 CMP #$A4:BNE ALOOP2
830 RENBING2 JSR CHRGET:BCS ALOOP2
840 ;
850 JSR SINTVAL:JSR SGETPTR2:JSR CHRGOT:JSR STRFAC:JSR FLFXD
860 ;
870 JSR CKRANGE:BEQ NEEDCHNG
880 JMP AFTERNUM
890 ;
900 ;
910 NEEDCHNG LDA INTVAL:LDX INTVAL+1
920 STA RENENDLN:STX RENENDLN+1
930 JSR COUNTEM:BEQ SOMELAB:LDA #$FF:STA RENCOUNT:STA RENCOUNT+1
940 SOMELAB JSR SPACE
950 ;
960 JSR MULT
970 LDX RENTEMP:LDA RENTEMP+1
980 STX INTVAL:STA INTVAL+1
990 JSR PRINTLN
1000 LDY RENTEMP:LDA RENTEMP+1:JSR FXDFL
1010 ;
1020 JSR RGETPTR3:LDX #$FF
1030 ;
1040 DOITLOOP LDY #0:INX
1050 LDA GETPTR+1:CMP TEMPC+1
1060 BCC NOPROB2:BNE DONETHIS
1070 LDA GETPTR:CMP TEMPC
1080 BCS DONETHIS
1090 NOPROB2 LDA (GETPTR),Y:STA BUFF,X:INY
1100 JSR UPDATE:JMP DOITLOOP
1110 DONETHIS LDY #0:LDA $0100:CMP #"-":BNE BIGLOOP:STA BUFF,X:INX
1120 BIGLOOP LDA $0101,Y:BEQ DONEPT2:STA BUFF,X:INY:INX:BNE BIGLOOP
1130 DONEPT2 STX TEMPH:JSR RGETPTR2:JSR CHRGOT:JSR STRFAC:JSR CHRGOT
1140 LDX TEMPH:LDY #0:BIG2LOOP CMP #0:STA BUFF,X:BEQ DONEPT3
1150 INX:INY:LDA (GETPTR),Y:JMP BIG2LOOP
1160 ;
1170 DONEPT3 INX:INX:STA BUFF,X:INX:INX:INX:STX $0B
1180 JSR RINTVAL:JSR PUTIN
1190 JSR RGETPTR2:JSR STRFAC
1200 LDA STRPTR:LDX STRPTR+1
1210 STA INTVAL:STX INTVAL+1
1220 JSR FINDLN:LDA VARPTR:LDX VARPTR+1
1230 STA RENGET:STX RENGET+1
1240 ;
1250 AFTERNUM JSR RINTVAL
1260 ;
1270 JSR CHRGOT:CMP #",":BEQ GETNEXT1
1280 CMP #$AB:BEQ GETNEXT1
1290 LDY #0:STY QUMODE:JMP ALOOP2
1300 ;
1310 GETNEXT1 JMP RENBING2
1320 ;
1330 ;
1340 ;
1350 GETFXD JSR INPFXD:GETINT LDA INTVAL
1360 LDX INTVAL+1:RTS
1370 ;
1380 ;
1390 MULT LDA RENINC:STA TEMPG:LDA RENSTART:LDX RENSTART+1
1400 STA RENTEMP:STX RENTEMP+1
1410 LDA #0:STA MULTFLAG
1420 ;
1430 LDX #8
1440 MULTLOOP LSR RENINC
1450 BCC DONTADD
1460 LDA MULTFLAG:BNE MULTERR
1470 LDA RENCOUNT
1480 CLC:ADC RENTEMP
1490 STA RENTEMP
1500 LDA RENCOUNT+1
1510 ADC RENTEMP+1
1520 STA RENTEMP+1
1530 ;
1540 DONTADD ASL RENCOUNT
1550 ROL RENCOUNT+1
1560 ROL MULTFLAG
1570 DEX
1580 BNE MULTLOOP
1590 ;
1600 LDA TEMPG:STA RENINC
1610 RTS
1620 MULTERR LDA #$FF
1630 STA RENTEMP:STA RENTEMP+1:LDA TEMPG:STA RENINC
1640 RTS
1650 ;
1660 PASS2 LDA RENGET:LDX RENGET+1
1670 ;
1680 STA GETPTR:STX GETPTR+1
1690 PASS2LP LDY #1:LDA (GETPTR),Y:BEQ PASS2DN
1700 LDY #3:LDA (GETPTR),Y
1710 CMP ENDPTR+1:BCC NOPROB:BNE PASS2DN
1720 DEY:LDA (GETPTR),Y
1730 CMP ENDPTR:BCC NOPROB:BNE PASS2DN
1740 ;
1750 NOPROB LDY #2:LDA RENSTART
1760 STA (GETPTR),Y
1770 INY:LDA RENSTART+1
1780 STA (GETPTR),Y
1790 ;
1800 LDY #0:LDA (GETPTR),Y:TAX
1810 INY:LDA (GETPTR),Y
1820 STA GETPTR+1:STX GETPTR
1830 LDA RENINC
1840 CLC:ADC RENSTART:STA RENSTART
1850 BCC PASS2LP
1860 LDA RENSTART+1:ADC #0:STA RENSTART+1
1870 JMP PASS2LP
1880 ;
1890 PASS2DN JMP READY
1900 ;
1910 ;
1920 SERROR9 JMP SERROR
1930 ;
1940 MEMORY LDA #$FF:STA ENDPTR:STA ENDPTR+1
1950 DEC GETPTR:LDA GETPTR:CMP #$FF:BNE NOSUBT
1960 DEC GETPTR+1
1970 NOSUBT JSR INPADD
1980 STA STRPTR:STX STRPTR+1
1990 JSR UPDATE:JSR CHRGET:BEQ GETGOING:CMP #",":BNE SERROR9
2000 JSR INPADD
2010 STA ENDPTR:STX ENDPTR+1
2020 ;
2030 GETGOING JSR CGRET
2040 LLOOPP JSR STOPSHFT:BEQ PASS2DN
2050 INC BLOCK
2060 LDA STRPTR+1:CMP BLOCK:BCC LETEM1
2070 LDA BLOCK:CLC:ADC #$10:STA TEMPH:LDY STRPTR+1:CPY TEMPH:BCS LETEM1
2080 ;
2090 DEC BLOCK
2100 JMP READY
2110 LETEM1 DEC BLOCK:LDA #".":JSR PRINT
2120 LDA STRPTR+1:LDX STRPTR
2130 JSR PRINTADD
2140 LDY #0
2150 PRINTMOR JSR SPACE
2160 LDA (STRPTR),Y
2170 STY TEMPH:JSR PRINTBYT:LDY TEMPH
2180 INY:CPY #8:BNE PRINTMOR
2190 JSR SPACE
2200 LDA #"{RVS ON}":JSR PRINT
2210 LDY #0
2220 BYTLOOP LDA (STRPTR),Y
2230 LDX #0:STX QUMODE
2240 CMP #161:BCS OOKK
2250 CMP #32:BCC NOTOOKK
2260 CMP #128:BCC OOKK
2270 NOTOOKK LDA #$2E
2280 OOKK JSR PRINT
2290 INY:CPY #8:BNE BYTLOOP
2300 ;
2310 TYA:CLC:ADC STRPTR:STA STRPTR
2320 LDA STRPTR+1:ADC #0:STA STRPTR+1
2330 JSR CGRET
2340 ;
2350 ;
2360 LDA STRPTR+1:CMP ENDPTR+1:BCC GOLLOOPP
2370 BNE NOLLOOPP
2380 ;
2390 LDA STRPTR:CMP ENDPTR:BCC GOLLOOPP
2400 BNE NOLLOOPP
2410 ;
2420 GOLLOOPP JMP LLOOPP
2430 NOLLOOPP JMP READY
2440 ;
2450 ;PUT THE LINE IN
2460 PUTIN JSR FINDLN    ; FIND BASIC LINE
2470 BCC L1
2480 LDY #1:LDA ($5F),Y
2490 STA $23:LDA $2D:STA $22
2500 LDA $60:STA $25:LDA $5F
2510 DEY:SBC ($5F),Y
2520 CLC:ADC $2D
2530 STA $2D:STA $24
2540 LDA $2E:ADC #$FF
2550 STA $2E:SBC $60
2560 TAX
2570 SEC:LDA $5F
2580 SBC $2D:TAY
2590 BCS L2
2600 INX:DEC $25
2610 L2 CLC:ADC $22
2620 BCC L3
2630 DEC $23:CLC
2640 L3 LDA ($22),Y
2650 STA ($24),Y
2660 INY:BNE L3
2670 INC $23:INC $25
2680 DEX:BNE L3
2690 L1 JSR BACKTEXT:JSR NEW ; UNDER [ NEW ]
2700 JSR RECHAIN
2710 LDA BUFF
2720 BEQ RETRN
2730 CLC:LDA $2D
2740 STA $5A:ADC $0B
2750 STA $58:LDY $2E
2760 STY $5B
2770 BCC L4
2780 INY
2790 L4 STY $59
2800 JSR MOVEMEM ; MOVE MEMORY
2810 LDA INTVAL:LDY INTVAL+1
2820 STA $01FE:STY $01FF
2830 LDA $31:LDY $32  ;END BASIC
2840 STA $2D:STY $2E  ;START BASIC VARS
2850 LDY $0B
2860 DEY
2870 L5 LDA $01FC,Y:STA ($5F),Y
2880 DEY:BPL L5
2890 JSR BACKTEXT:JSR NEW ; UNDER [ NEW ]
2900 JSR RECHAIN
2910 ;
2920 RETRN RTS
2930 ;
2940 NEW LDA $37:LDY $38
2950 STA $33:STY $34
2960 LDA $2D:LDY $2E
2970 STA $2F:STY $30
2980 STA $31:STY $32
2990 JSR $A81D
3000 LDX #$19:STX $16
3010 LDA #0:STA $3E:STA $10
3020 RTS
3030 ;
3040 COUNTEM LDA RENGET:LDX RENGET+1
3050 STA CNTPTR:STX CNTPTR+1
3060 LDY #0:STY RENCOUNT:STY RENCOUNT+1
3070 ;
3080 COUNTLP LDY #1:LDA (CNTPTR),Y
3090 BNE CNTNOVER:LDA #1:RTS
3100 ;
3110 CNTNOVER LDY #3:LDA (CNTPTR),Y
3120 CMP RENENDLN+1
3130 BCC CNTON
3140 BNE CNTDONE
3150 DEY:LDA (CNTPTR),Y
3160 CMP RENENDLN
3170 BCC CNTON
3180 ;
3190 CNTDONE RTS
3200 ;
3210 CNTON LDY #0
3220 LDA (CNTPTR),Y:TAX
3230 INY:LDA (CNTPTR),Y:STA CNTPTR+1:STX CNTPTR
3240 INC RENCOUNT:BNE COUNTLP
3250 INC RENCOUNT+1:BNE COUNTLP
3260 ;
3270 SINTVAL LDA INTVAL:LDX INTVAL+1
3280 STA TEMPA:STX TEMPB
3290 RTS
3300 ;
3310 RINTVAL LDA TEMPA:LDX TEMPB
3320 STA INTVAL:STX INTVAL+1
3330 RTS
3340 ;
3350 SGETPTR2 LDA GETPTR:LDX GETPTR+1
3360 STA TEMPC:STX TEMPC+1
3370 RTS
3380 ;
3390 RGETPTR2 LDA TEMPC:LDX TEMPC+1
3400 STA GETPTR:STX GETPTR+1
3410 RTS
3420 ;
3430 CKRANGE LDA INTVAL+1:CMP STRPTR+1
3440 BEQ ISEQU
3450 BCS CKENDPTR
3460 NOTRNGE LDA #1:RTS
3470 INRANGE LDA #0:RTS
3480 ;
3490 ISEQU LDA INTVAL:CMP STRPTR
3500 BCC NOTRNGE
3510 ;
3520 CKENDPTR LDA INTVAL+1:CMP ENDPTR+1
3530 BCC INRANGE
3540 BNE NOTRNGE
3550 LDA INTVAL:CMP ENDPTR
3560 BCC INRANGE
3570 BEQ INRANGE
3580 BCS NOTRNGE
3590 ;
3600 FXDFL CMP #$80:BCC SKP32768
3610 AND #$7F
3620 JSR SFXDFL:LDA #<CONSTANT:LDY #>CONSTANT
3630 JSR ADDEM
3640 JMP STRIT
3650 ;
3660 SKP32768 JSR SFXDFL
3670 STRIT JSR CONVFAC1
3680 RTS
3690 ;
3700 SGETPTR3 LDA GETPTR:LDX GETPTR+1
3710 STA TEMPI:STX TEMPI+1
3720 RTS
3730 ;
3740 RGETPTR3 LDA TEMPI:LDX TEMPI+1
3750 STA GETPTR:STX GETPTR+1
3760 RTS
3770 ;
3780 BONJOUR LDA #"?":STA DELIM:LDX #17:STX FILELEN
3790 JSR CHRGOT:JSR INPFILE
3800 JSR CGRET
3810 BONGET JSR GET:BEQ BONGET:CMP #",":BEQ LESAVE
3820 JMP READY
3830 ;
3840 LESAVE LDA 646:STA TEMPG:LDA 53281:STA 646
3850 LDY #0:LDA SAVETBLE+1:STA TEMPJ:STY SAVETBLE+1
3860 LDA SAVETBLE+2:STA TEMPJ+1:STY SAVETBLE+2
3870 JSR SAVEEE2
3880 LDA TEMPJ:STA SAVETBLE+1
3890 LDA TEMPJ+1:STA SAVETBLE+2
3900 LDA #147:JSR PRINT
3910 LDA TEMPG:STA 646
3920 JMP DS
3930 ;
3940 BLOCK .BYTE>BLOCKK-1
3950 ;
3960 RENTBLE .BYTE $8A,$9B,$CB,$A7,$8D,$89
3970 ;RUN,LIST,GO,THEN,GOSUB,GOTO
3980 RENMSGE1 .ASC " ** BAD LINE RANGE ** ":.BYTE 13,0
3990 CONSTANT .BYTE $90,0,0,0,0
4000 ;
4010 NAMESTR .ASC "      ":.BYTE 0
4020 ;
4030 TOKTBLE .ASC "ADJUST":.BYTE 0
4040 .ASC "AUTO":.BYTE 0
4050 .ASC "COLD":.BYTE 0
4060 .ASC "COLOUR":.BYTE 0
4070 .ASC "DEL":.BYTE 0
4080 .ASC "DS":.BYTE 0
4090 .ASC "HELP":.BYTE 0
4100 .ASC "HEX":.BYTE 0
4110 .ASC "HUNT":.BYTE 0
4120 .ASC "LOOK":.BYTE 0
4130 .ASC "MEM":.BYTE 0
4140 .ASC "MERGE":.BYTE 0
4150 .ASC "N":.BYTE 0
4160 .ASC "OFF":.BYTE 0
4170 .ASC "RENUM":.BYTE 0
4180 .ASC "SAVE":.BYTE 0
4190 .ASC "SEND":.BYTE 0
4200 .ASC "START":.BYTE 0
4210 .ASC "{dolr}":.BYTE 0
4220 .ASC "/":.BYTE 0
4230 .ASC "{up arrow}HI":.BYTE 0
4240 .BYTE 0
4250 ;
4260 COMMANDS .BYTE 13:.ASC " COMMANDS :":.BYTE 13
4270 .ASC " ---------- ":.BYTE 13,0
4280 ;
4290 STMSGE .ASC "{DOWN}MERGING LINE: "
4300 .BYTE 0
4310 CRIGHT .ASC "{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}"
4320 .BYTE 0
4330 ;
4340 USERSTRG .ASC ",8:":.BYTE0
4350 ;
4360 REVERSE .ASC "{RVS ON}                    {RVS OFF}"
4370 .BYTE 13,0
4380 COLTBLE .ASC "{BLK}{WHT}{RED}{CYN}{pur}{GRN}{BLU}{YEL}{ORN}{BRW}{PNK}{DGRY}{GRY}{LGRN}{LBLU}{LGRY}"
4390 ;
4400 OURMSGE .ASC " ** INVALID WEDGE COMMAND ** ":.BYTE 13,0
4410 HELLO .BYTE 13:.ASC " WEDGE-64 (C)1983 D.MASON & A.WUNSCHE ":.BYTE 13
4420 .ASC " MERGE ADAPTED FROM BASIC AID ":.BYTE 13
4430 .ASC " PERMISSION TO USE BUT NOT TO SELL ":.BYTE 13,0
4440 OFFF .BYTE 13:.ASC " WEDGE-64 DISABLED ":.BYTE 13,0
4450 ;
4460 FILENAME *= *+55
4470 TOKNUM *=*+1
4480 TEMPX *=*+1
4490 TEMPY *=*+1
4500 TOKVCTR *=*+2
4510 FILELEN *=*+1
4520 TEMP8 *=*+1
4530 DELIM *=*+1
4540 STRLEN *= *+1
4550 TEMPA *= *+1:TEMPB *= *+1
4560 INCR *= *+1
4570 RENSTART *= *+2
4580 RENGET *= *+2
4590 TEMPC *= *+2
4600 RENENDLN *= *+2
4610 TEMPG *= *+1:TEMPH *= *+1
4620 TEMPI *= *+2:TEMPJ *= *+2
4630 ;
4640 ;
50000 .END 8,"w-64a.e8"