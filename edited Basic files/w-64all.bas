10 OPEN 4,8,1,"w-64.prg"
100 REM WEDGE-64
110 REM BY DARCY MASON & ALAN WUNSCHE
120 :
130 SYS 700:*= $C000:BLOCKK = *:.OPT O4
150 JMP BEGIN:JMP OFF
160 ;
170 GETPTR = $7A
180 SYNERROR = $AF08
190 CHRGET = $0073
200 SHIFTKEY = $028D
210 CRUNCH = $A579
220 CHRGOT = $0079
230 PRINT = $FFD2
240 PRINT2 = $E716
250 RREADY = $A474
260 STROUT = $AB1E
270 STOPKEY = $FFE1
280 INPUT = $FFCF
290 TALK = $FFB4
300 SERIN = $FFCF
310 UNLSN = $FFAE
320 UNTALK = $FFAB
330 SYSLOAD = $E175
340 GET = $FFE4
350 NUMCHAR = $B7
360 SETLFS = $FFBA
370 SETNAM = $FFBD
380 OPEN = $FFC0
390 CHKIN = $FFC6
400 ST = $90
410 PRINTLN = $BDCD
420 CLOSE = $FFC3
430 INTVAL = $14
440 BUFF = $0200
450 MOVEMEM = $A3B8
460 CLALL = $FFE7
470 RECHAIN = $A533
480 BACKTEXT = $A68E
490 COMMA = $AEFD
500 EVALEXP = $AD9E
510 FLFXD = $B7F7
520 CALLADD = $A48C
530 DEVNP = 128
540 ERROREX = $A437
550 TOM = $37
560 RENTEMP = $8D
570 RENINC = $28
580 MULTFLAG = $2A
590 RENCOUNT = $8B
600 QUMODE = $08
610 STRFAC = $BCF3
620 VARSTART = $2D:SAVEPROG = $E15F
630 STARRAY = $2F
640 LOADFAC1 = $BBA2
650 CONVFAC1 = $BDDD
660 VARPTR = $5F
670 STRPTR = $FD
680 ENDPTR = $26
690 INPFXD = $A96B
700 FINDLN = $A613
710 ILLQTY = $B248
720 RECHNPTR = $22
730 MOVETO = $FD
740 MOVEFROM = $26
750 SFXDFL = $B391
760 KEYB = 631
770 KEYBLEN = 198
780 FOUNDNUM = $A49F:STARTB = $2B
790 ADDEM = $B867
800 CNTPTR = $19
810 MEMFAC2 = $BA8C
820 ;
830 ;
840 BEGIN JSR INIT
850 READY JMP RREADY
860 ;
870 ;
880 ;
890 ;
900 GETCH2 INC GETPTR:BNE NOT0:INC GETPTR+1
910 NOT0 JSR CHRGOT:STX TEMPX:STY TEMPY
920 TSX:LDA $0101,X:CMP #<CALLADD:BNE OLDGET
930 LDA $0102,X:CMP #>CALLADD:BEQ GOODCALL
940 OLDGET LDX TEMPX:LDY TEMPY:JMP CHRGOT
950 CKAUTO JSR AUTOLN:JMP CHRGOT
960 GOODCALL JSR CHRGOT:BCC CKAUTO
970 ;
980 ;
990 CMP #">":BNE OLDGET
1000 JSR CHRGET:PLA:PLA
1010 ;
1020 ; CHECK FOR VALID COMMANDS
1030 ;
1040 LDX #0:STX TOKNUM
1050 USERTOK LDY #0
1060 TOKLOOP LDA TOKTBLE,X:BEQ GOODTOK:CMP (GETPTR),Y
1070 BNE NEXTTOK:INX:INY:BNE TOKLOOP
1080 ;
1090 NEXTTOK INX:LDA TOKTBLE,X:BNE NEXTTOK:INX:INC TOKNUM
1100 LDA TOKTBLE,X:BEQ SERROR2:BNE USERTOK
1110 ;
1120 SERROR2 JMP OURERR
1130 ;
1140 ;
1150 GOODTOK JSR UPDATE
1160 LDA TOKNUM:ASL:TAX:LDA TOKADD,X:STA TOKVCTR:LDA TOKADD+1,X
1170 STA TOKVCTR+1
1180 JSR CHRGOT:JMP (TOKVCTR)
1190 ;
1200 ;
1210 TOKADD .WORD ADJUST
1220 .WORD AUTO
1230 .WORD COLD
1240 .WORD COL
1250 .WORD DEL
1260 .WORD DS
1270 .WORD HELP
1280 .WORD HEX
1290 .WORD HUNT
1300 .WORD DUMP
1310 .WORD MEMORY
1320 .WORD MERGE
1330 .WORD NAME
1340 .WORD OFF
1350 .WORD RENUM
1360 .WORD SAVEM
1370 .WORD SEND
1380 .WORD START
1390 .WORD DISK
1400 .WORD LOAD
1410 .WORD BONJOUR
1420 ;
1430 ;
1440 MERGE LDX #17:STX FILELEN:JSR INPFILE
1450 ;
1460 JSR STRTMSGE
1470 LDA #2:LDX #8:LDY #0:JSR OPENFILE
1480 CONT1 LDX #2:JSR CHKIN
1490 LDX #0
1500 JSR GETCHR:JSR GETCHR ;LOAD ADDRESS
1510 LNLOOP JSR GETCHR:JSR GETCHR ;LINK
1520 CMP #0:BNE CONT2
1530 JMP PGMOVER
1540 CONT2 JSR GETLN
1550 ;
1560 ;PUT THE LINE IN
1570 JSR PUTIN
1580 JMP LNLOOP
1590 ;
1600 PGMOVER JSR CGRET
1610 JMP DS
1620 ;
1630 ; GET A BASIC LINE
1640 ;
1650 GETLN LDX #255:JSR GETCHR
1660 STA INTVAL:JSR GETCHR:STA INTVAL+1
1670 STX TEMPX:LDA INTVAL+1:LDX INTVAL:JSR PRINTLN:JSR CGRET
1680 LDA #"{UP}":JSR PRINT:LDA #<CRIGHT:LDY #>CRIGHT:JSR STROUT
1690 LDX TEMPX
1700 LL5 INX:CPX #$4E:BCS LL4
1710 JSR GETCHR:STA BUFF,X:CMP #0:BNE LL5
1720 LL4 INX:INX
1730 STA BUFF,X
1740 INX:INX:INX
1750 STX $0B      ; BUFF PTR
1760 RTS
1770 ;
1780 SERROR LDA #34:STA DELIM:JMP SYNERROR
1790 INPFILE LDX #0:LDY #0
1800 CMP DELIM:BNE SERROR
1810 FILELOOP CPX FILELEN:BEQ STRERR:JSR CHRGET2:BEQ NAMEDONE
1820 CMP DELIM:BEQ NAMEDON
1830 STA FILENAME,X:INX:BNE FILELOOP
1840 NAMEDON INY
1850 NAMEDONE CPX #0:BEQ SERROR:STX NUMCHAR:LDA #0:STA FILENAME,X
1860 JSR UPDATE
1870 LDX #<FILENAME:LDY #>FILENAME
1880 LDA NUMCHAR:JSR SETNAM
1890 LDA #34:STA DELIM:JMP CHRGOT
1900 ; STRING TOO LONG MESSAGE
1910 STRERR LDA #34:STA DELIM:LDX #23:JMP ERROREX
1920 ;
1930 ; ERROR ROUTINES
1940 ;
1950 STERROR CPY #64:BNE CONTT:JMP PGMOVER
1960 CONTT JMP DS
1970 ;
1980 GETCHR JSR INPUT:LDY ST:BNE STERROR
1990 RTS
2000 STRTMSGE LDA #<STMSGE:LDY #>STMSGE
2010 JMP STROUT
2020 CHRGET2 INY:LDA (GETPTR),Y:RTS
2030 ;
2040 ;
2050 LOAD LDX #17:STX FILELEN:JSR INPFILE
2060 LDA #1:LDX #8:LDY #1
2070 JSR SETLFS
2080 LDA #0:STA $0A:JMP SYSLOAD
2090 ;
2100 DS LDA #1:JSR CLOSE:LDA #2:JSR CLOSE:JSR CLALL
2110 LDA #32:JSR PRINT
2120 LDA #0:JSR SETNAM:JSR OPENCC
2130 LDX #1:JSR CHKIN
2140 DSLOOP JSR INPUT:JSR PRINT:CMP #13:BNE DSLOOP:LDA #" ":JSR PRINT2
2150 LDA #1:JSR CLOSE:JSR CLALL:JMP READY
2160 ;
2170 OFF LDA #<OFFF:LDY #>OFFF:JSR STROUT:JSR RESGETT:JMP READY
2180 ;
2190 RESGETT LDX #2:RESGET LDA SAVETBLE,X:STA CHRGET,X
2200 LDA #0:STA SAVETBLE,X:DEX
2210 BPL RESGET:RTS
2220 ;
2230 START LDX #17:STX FILELEN:JSR INPFILE
2240 LDA #2:LDX #8:LDY #$0:JSR OPENFILE
2250 LDX #2:JSR CHKIN
2260 JSR INPUT; LOAD ADDRESS LO-BYTE
2270 JSR TIMER
2280 TAX:STX TEMPX:JSR INPUT; HI-BYTE
2290 JSR TIMER
2300 STA TEMPY:LDA #2:JSR CLOSE:JSR CLALL
2310 LDA #"$":JSR PRINT
2320 LDA TEMPY:LDX TEMPX:JSR PRINTADD
2330 JSR SPACE:JSR PRINT
2340 LDA TEMPY:LDX TEMPX:JSR PRINTLN
2350 JSR SPACE
2360 JSR CGRET:JMP DS
2370 TIMER LDY #100:TIMELOOP DEY:BNE TIMELOOP:RTS
2380 ;
2390 SEND LDX #80:STX FILELEN:JSR INPFILE
2400 JSR OPENCC:JMP DS
2410 ;
2420 HELP LDA #<COMMANDS:LDY #>COMMANDS:JSR STROUT
2430 LDY #0
2440 LABEL LDA TOKTBLE,Y:CMP #"{up arrow}":BEQ END2:JSR SPACE:LDA #">"
2450 JSR PRINT:HELPLOOP LDA TOKTBLE,Y
2460 BEQ END1
2470 NOTUPARR JSR PRINT:INY:BNE HELPLOOP
2480 END1 JSR CGRET
2490 END3 INY:LDA TOKTBLE,Y
2500 BNE LABEL
2510 JMP READY
2520 END2 INY:LDA TOKTBLE,Y:BNE END2:JMP END3
2530 ;
2540 ; INITIALIZE
2550 ;
2560 INIT LDA BLOCK:CMP #$A0:BCS OVERIT
2570 CMP TOM+1:BCS OVERIT:STA TOM+1:LDA #255:STA TOM:JSR NEW
2580 ;
2590 OVERIT LDA #0:STA INCR:LDA #34:STA DELIM
2600 LDX #2:LDA #2:STA 53280:LDA #15:STA 53281:LDA #0:STA 646
2610 LDA SAVETBLE,X:BNE HELLOO
2620 SAVEGET LDA CHRGET,X:STA SAVETBLE,X:DEX:BPL SAVEGET
2630 LDX #2:INITGET LDA GETTBLE,X:STA CHRGET,X:DEX:BPL INITGET
2640 HELLOO LDA #<HELLO:LDY #>HELLO:JMP STROUT
2650 ;
2660 SAVETBLE .BYTE 0,0,0,0
2670 GETTBLE .BYTE $4C
2680 .WORD GETCH2
2690 OURERR LDA #<OURMSGE:LDY #>OURMSGE
2700 JSR STROUT:JMP READY
2710 ;
2720 OPENCC LDA #1
2730 LDX #8:LDY #15:OPENFILE JSR SETLFS
2740 JSR OPEN; COMMAND CHANNEL OPEN !
2750 LDY ST
2760 BEQ OPENED
2770 CMP #DEVNP:BNE JUMPDS:LDX #5:JMP ERROREX:JUMPDS JMP DS
2780 OPENED RTS
2790 ;
2800 ;
2810 PRINTNIB CLC:ADC #$F6:BCC ADD2
2820 ADC #$06:ADD2 ADC #$3A:JMP PRINT
2830 ;
2840 PRINTBYT PHA:LSR:LSR:LSR:LSR:JSR PRINTNIB
2850 PLA:AND #$0F:JMP PRINTNIB
2860 ;
2870 PRINTADD JSR PRINTBYT:TXA:JMP PRINTBYT
2880 ;
2890 OUROPEN JSR OPEN:LDY ST:BEQ OKST:JMP DS
2900 OKST RTS
2910 ;
2920 ;
2930 HEX JSR CRUNCH:JSR CHRGET:CMP #"$":BEQ HEX2:JSR EVALEXP:JSR FLFXD
2940 JSR SPACE:LDA #"$":JSR PRINT:
2950 LDX INTVAL:LDA INTVAL+1
2960 JSR PRINTADD
2970 HEXEND JSR SPACE:JSR CGRET:JMP READY
2980 ;
2990 ABCDEF JMP SYNERROR
3000 HEX2 JSR INPADD
3010 STA TEMPX:JSR SPACE:JSR CHRGET2:BNE ABCDEF:TXA:LDX TEMPX:JSR PRINTLN
3020 JMP HEXEND
3030 ;
3040 DISK STA FILENAME+1
3050 LDY #255:LDA #"$":STA FILENAME
3060 CATLOOP2 JSR CHRGET2:STA FILENAME+1,Y
3070 BEQ CNAMEOK2:CPY #49:BNE CATLOOP2:JMP STRERR
3080 CNAMEOK2 INY:STY NUMCHAR:INY:LDA #0:STA FILENAME+2,Y:LDA #1:LDX #8
3090 LDY #$60
3100 JSR SETLFS:JSR CGRET
3110 JSR UNLSN:JSR UNTALK:LDA NUMCHAR:LDX #<FILENAME:LDY #>FILENAME
3120 JSR SETNAM:JSR OPEN:LDX #1:JSR CHKIN
3130 JSR SERIN:JSR SERIN;  IGNORE LOAD ADDRESS
3140 GETLINK2 JSR SERIN:JSR SERIN:LDX ST:BNE CATDONE ; IGNORE LINK
3150 LDA #32:JSR PRINT2
3160 JSR SERIN:TAX:JSR SERIN:JSR PRINTLN   ; PRINT PGM  # BLOCKS
3170 LDA #" ":JSR PRINT2:JSR PRINT2:JSR PRINT2:JSR PRINT2
3180 DIRLOOP2 JSR SERIN:JSR PRINT2:BNE DIRLOOP2
3190 LDA #13:JSR PRINT2:JMP GETLINK3
3200 ;
3210 CATDONE JSR UNTALK:JSR CGRET:JSR PRINT
3220 JMP DS
3230 ;
3240 GETLINK3 JSR SERIN:JSR SERIN:LDX ST:BNE CATDONE ; IGNORE LINK
3250 LDA #32:JSR PRINT2
3260 JSR SERIN:TAX:JSR SERIN:JSR PRINTLN   ; PRINT PGM  # BLOCKS
3270 LDA #32:JSR PRINT2
3280 DIRLOOP3 JSR SERIN:JSR PRINT2:BEQ CATDONE:CMP #34:BNE DIRLOOP3
3290 LDY #18
3300 CALNLOOP JSR SERIN:CMP #0:BEQ CATDONE:JSR PRINT2:DEY:BNE CALNLOOP
3310 LDA #" ":JSR PRINT2:JSR PRINT2
3320 LDA #":":JSR PRINT
3330 CALNLP2 JSR SERIN:JSR PRINT2:BNE CALNLP2
3340 JSR STOPSHFT:BEQ CATDONE
3350 LDA #13:JSR PRINT2:JMP GETLINK3
3360 ;
3370 NAME CMP #34:BEQ USERNAME
3380 LDA #<NAMESTR:LDY #>NAMESTR:JSR STROUT
3390 ;
3400 LDA #34:JSR PRINT
3410 LDA #<FILENAME:LDY #>FILENAME:JSR STROUT
3420 LDA #34:JSR PRINT
3430 LDA #<USERSTRG:LDY #>USERSTRG:JSR STROUT
3440 LDA #"1":JSR PRINT
3450 LDX #$FF:TXS:JMP RREADY+12
3460 ;
3470 USERNAME LDX #70:STX FILELEN:JSR INPFILE:JMP READY
3480 INPADD LDA #0:STA TEMPY:TAY:JSR INPBYTE
3490 TAX:STX TEMP8:LDA #0:STA TEMPY
3500 INPBYTE JSR INPNIB
3510 ASL:ASL:ASL:ASL
3520 STA TEMPY
3530 INPNIB JSR CHRGET2
3540 CMP #"0":BCC SERROR3
3550 CMP #":":BCC O9
3560 CMP #"A":BCC SERROR3
3570 CMP #"G":BCS SERROR3
3580 SBC #$06
3590 O9 AND #$0F
3600 ORA TEMPY
3610 LDX TEMP8
3620 RTS
3630 SERROR3 JMP SYNERROR
3640 COLD LDA #0:LDX #2:COLDLOOP STA SAVETBLE,X:DEX:BPL COLDLOOP:JMP 64738
3650 SPACE LDA #" ":JMP PRINT
3660 CGRET LDA #13:JMP PRINT
20100 ;
20110 ; W-64b - FILE#2
20120 ;
20130 FINI JMP READY
20140 DUMP LDA VARSTART:LDX VARSTART+1
20150 STA VARPTR:STX VARPTR+1:JMP GETVAR
20160 ;
20170 NEXTVAR JSR STOPSHFT:BEQ FINI:JSR CGRET
20180 LDA #7:CLC:ADC VARPTR:STA VARPTR
20190 LDA VARPTR+1:ADC #0:STA VARPTR+1
20200 ;
20210 GETVAR JSR SPACE:LDA VARPTR+1
20220 CMP STARRAY+1:BCC DUMPOK
20230 BNE FINI
20240 LDA VARPTR
20250 CMP STARRAY:BCS FINI
20260 ;
20270 DUMPOK LDY #0:LDA (VARPTR),Y
20280 BPL NOTINT
20290 JMP INTEGER
20300 NOTINT JSR PRINT
20310 JSR INCPTR
20320 CMP #$80
20330 BCS STRING
20340 FLOATING JSR PRINT:JSR EQUALS:LDA #"{LEFT}":JSR PRINT
20350 JSR INCPTR
20360 TYA:CLC:ADC VARPTR:STA TEMPX:LDA VARPTR+1:ADC #0:STA TEMPY
20370 LDA TEMPX:LDY TEMPY:JSR LOADFAC1
20380 JSR CONVFAC1
20390 JSR STROUT
20400 JMP NEXTVAR
20410 ;
20420 STRING AND #$7F:JSR PRINT:LDA #"$":JSR PRINT
20430 JSR EQUALS:LDA #34:JSR PRINT
20440 JSR INCPTR:STA STRLEN
20450 JSR INCPTR:STA STRPTR
20460 JSR INCPTR:STA STRPTR+1
20470 LDY STRLEN:BEQ QUOTE
20480 LDY #0:PRLOOP LDA (STRPTR),Y
20490 JSR PRINT:INY:CPY STRLEN:BNE PRLOOP
20500 QUOTE LDA #34:JSR PRINT
20510 JMP NEXTVAR
20520 ;
20530 INTEGER AND #$7F:JSR PRINT
20540 JSR INCPTR:AND #$7F:JSR PRINT
20550 LDA #"%":JSR PRINT:JSR EQUALS
20560 JSR INCPTR:STA TEMPA ; GET HI BYTE
20570 ; HAS HIGH BIT SET IF NEG'VE
20580 BPL PLUS
20590 LDA #"-":JSR PRINT
20600 LDA TEMPA:EOR #$FF:CLC:ADC #0
20610 PHA:JSR INCPTR:EOR #$FF:TAX:INX:BNE PULL:PLA:CLC:ADC #1:JMP PRINTIT
20620 PULL PLA:JMP PRINTIT
20630 PLUS LDA TEMPA:AND #$7F
20640 PHA:JSR INCPTR
20650 TAX
20660 PLA
20670 PRINTIT JSR PRINTLN
20680 JMP NEXTVAR
20690 ;
20700 INCPTR INY:LDA (VARPTR),Y
20710 RTS
20720 EQUALS JSR SPACE:LDA #"=":JSR PRINT:JMP SPACE
20730 ;
20740 ;  H U N T
20750 ;
20760 HUNT JSR CRUNCH:JSR CHRGET:LDX #80:STX FILELEN
20770 STA DELIM:JSR INPFILE
20780 ;
20790 BEQ ONLYDL
20800 JSR COMMA
20810 ONLYDL JSR LNRANGE
20820 ;
20830 LDY STRPTR:LDX STRPTR+1:STY INTVAL:STX INTVAL+1
20840 LDA #4:CLC:ADC NUMCHAR:STA NUMCHAR
20850 JSR FINDLN
20860 ;
20870 NEWLINE JSR STOPSHFT:BEQ ALLGONE
20880 LDY #1:LDA (VARPTR),Y  ;HB LINK
20890 BEQ ALLGONE
20900 ;
20910 INY:LDA (VARPTR),Y
20920 STA INTVAL          ; GET LN#
20930 INY:LDA (VARPTR),Y
20940 STA INTVAL+1
20950 ;
20960 LDA INTVAL+1
20970 CMP ENDPTR+1
20980 BCC HUNTLOOP     ;CHECK HB
20990 BNE ALLGONE
21000 ;
21010 LDA INTVAL
21020 CMP ENDPTR
21030 BEQ HUNTLOOP     ;CHECK LB
21040 BCS ALLGONE
21050 ;
21060 HUNTLOOP LDY #4
21070 KEEPTRY LDA (VARPTR),Y
21080 BEQ NEXTLINE
21090 CMP FILENAME-4,Y
21100 BEQ OKSOFAR
21110 ;
21120 INC VARPTR
21130 BNE OKKKK
21140 INC VARPTR+1
21150 OKKKK JMP HUNTLOOP
21160 ;
21170 OKSOFAR INY   ;GOOD CHR->GET NEXT
21180 CPY NUMCHAR
21190 BNE KEEPTRY
21200 ;
21210 JSR FINDLN:JSR PRLINE   ;BINGO!
21220 ;
21230 ;
21240 NEXTLINE TYA:SEC
21250 ADC VARPTR        ;UPDATE PTR TO
21260 STA VARPTR
21270 BCC NEWLINE       ;NEXT LINE
21280 INC VARPTR+1
21290 JMP NEWLINE
21300 ;
21310 ALLGONE JMP READY
21320 ;
21330 PRLINE LDY #1:JSR SPACE
21340 STY $0F
21350 LDA (VARPTR),Y
21360 BEQ LAB1
21370 JSR $AAD7
21380 INY:LDA (VARPTR),Y
21390 TAX
21400 INY:LDA (VARPTR),Y
21410 STY $49
21420 JSR PRINTLN
21430 LDA #" "
21440 LAB8 LDY $49
21450 AND #$7F
21460 LAB4 JSR $AB47
21470 CMP #$22
21480 BNE LAB2
21490 LDA $0F
21500 EOR #$FF
21510 STA $0F
21520 ;
21530 LAB2 INY
21540 BEQ LAB1
21550 LDA (VARPTR),Y
21560 BNE LAB3
21570 ;
21580 LAB1 RTS        ;ALL DONE!
21590 ;
21600 LAB3 BPL LAB4
21610 CMP #$FF
21620 BEQ LAB4
21630 BIT $0F
21640 BMI LAB4
21650 ;
21660 SEC:SBC #$7F
21670 TAX
21680 STY $49
21690 LDY #$FF
21700 LAB7 DEX
21710 BEQ LAB5
21720 ;
21730 LAB6 INY:LDA $A09E,Y
21740 BPL LAB6
21750 BMI LAB7
21760 ;
21770 LAB5 INY:LDA $A09E,Y
21780 BMI LAB8
21790 JSR $AB47
21800 BNE LAB5
21810 ;
21820 STOPSHFT JSR STOPKEY
21830 BEQ GOBACK
21840 SHIFTON LDA SHIFTKEY
21850 CMP #1
21860 BEQ SHIFTON
21870 GOBACK RTS
21880 ;
21890 ILLEGAL JMP ILLQTY
21900 ;
21910 COL BEQ READYYYY:JSR GETFXD:BNE ILLEGAL
21920 STA 53280
21930 JSR COMMA
21940 JSR GETFXD:BNE ILLEGAL
21950 STA 53281
21960 JSR COMMA
21970 JSR GETFXD:BNE ILLEGAL
21980 STA 646
21990 READYYYY JMP READY
22000 ;
22010 ;
22020 ;
22030 TOOBIG JMP ILLQTY
22040 AUTO JSR GETFXD
22050 BNE TOOBIG
22060 STA INCR:JMP READY
22070 ;
22080 ;
22090 AUTOLN JSR INPFXD:LDY INCR:BEQ GOBACK4
22100 JSR CHRGOT:CMP #0:BEQ GOBACK4
22110 LDA INCR
22120 CLC:ADC INTVAL:TAY:LDA INTVAL+1:ADC #0
22130 JSR FXDFL
22140 LDX #0:KEYBLOOP LDA $0101,X:BEQ KEYBDONE:STA KEYB,X:INX:BNE KEYBLOOP
22150 KEYBDONE INX
22160 LDA #" ":STA KEYB,X
22170 INX:STX KEYBLEN
22180 GOBACK4 JMP FOUNDNUM
22190 ;
22200 DEL BEQ READYY
22210 JSR CRUNCH:JSR CHRGET:JSR LNRANGE
22220 LDA STRPTR:LDX STRPTR+1
22230 STA INTVAL:STX INTVAL+1
22240 JSR FINDLN
22250 LDA VARPTR:LDX VARPTR+1
22260 STA MOVETO:STX MOVETO+1
22270 LDX ENDPTR:LDY ENDPTR+1
22280 INX:BNE STORE:INY:STORE STX INTVAL:STY INTVAL+1
22290 JSR FINDLN:LDA VARPTR:LDX VARPTR+1:STA MOVEFROM:STX MOVEFROM+1
22300 ;
22310 JSR DELMEM
22320 JSR RECHAIN
22330 LDA RECHNPTR:CLC:ADC #2:STA VARSTART
22340 LDA RECHNPTR+1:ADC #0:STA VARSTART+1
22350 JSR NEW:JMP READY
22360 READYY JMP SYNERROR
22370 ;
22380 ;
22390 ;
22400 LNRANGE LDY #$FF
22410 STY ENDPTR+1
22420 INY:STY ENDPTR:STY STRPTR:STY STRPTR+1
22430 JSR CHRGOT:BEQ GOBACK2
22440 BCC PAR1
22450 CMP #$AB:BNE LASTLBL
22460 JSR CHRGET:BCS BADPAR:JMP PAR2
22470 ;
22480 BADPAR JMP SYNERROR
22490 ;
22500 PAR1 JSR INPFXD
22510 LDA INTVAL:LDX INTVAL+1
22520 STA STRPTR:STX STRPTR+1
22530 JSR CHRGOT:CMP #$AB:BNE BADPAR
22540 JSR CHRGET:BEQ GOBACK2
22550 BCC PAR2
22560 GOBACK2 RTS
22570 LASTLBL JMP SYNERROR
22580 ;
22590 PAR2 JSR INPFXD
22600 LDA INTVAL:LDX INTVAL+1
22610 STA ENDPTR:STX ENDPTR+1
22620 JMP CHRGOT
22630 DELMEM LDY #0
22640 DELLOOP LDA MOVEFROM+1:CMP VARSTART+1
22650 BCC STARTDEL
22660 BNE DELDONE
22670 LDA MOVEFROM:CMP VARSTART
22680 BCS DELDONE
22690 ;
22700 STARTDEL LDA (MOVEFROM),Y
22710 STA (MOVETO),Y
22720 INC MOVEFROM:BNE XXX
22730 INC MOVEFROM+1
22740 XXX INC MOVETO:BNE YYY
22750 INC MOVETO+1
22760 YYY BNE DELLOOP
22770 ;
22780 ;
22790 DELDONE RTS
22800 ;
22810 ;
22820 SGETPTR LDA GETPTR:LDX GETPTR+1
22830 STA VARPTR:STX VARPTR+1
22840 RTS
22850 ;
22860 RGETPTR LDA VARPTR:LDX VARPTR+1
22870 STA GETPTR:STX GETPTR+1
22880 RTS
22890 ;
22900 SAVEM JSR SAVEEE:JMP DS
22910 ;
22920 SAVEEE LDX #17:STX FILELEN
22930 JSR INPFILE
22940 SAVEEE2 LDA #1:LDX #8:TAY:JSR SETLFS
22950 ;
22960 JSR CHRGOT:CMP #",":BEQ MLSAVE
22970 LDA #STARTB:LDX VARSTART
22980 LDY VARSTART+1
22990 SAVEIT JSR SAVEPROG:JMP CGRET
23000 ;
23010 ;
23020 MLSAVE JSR INPADD
23030 STA STRPTR:STX STRPTR+1
23040 JSR UPDATE:JSR CHRGET
23050 CMP #",":BNE MLSERR
23060 JSR INPADD
23070 STX TEMPX:TAX:JSR UPDATE:LDY TEMPX
23080 LDA #STRPTR
23090 JMP SAVEIT
23100 ;
23110 MLSERR JMP SYNERROR
23120 ;
23130 ADJUST LDX #0:LDA 646:STA TEMPA
23140 COLLOOP STX TEMPB:LDA COLTBLE,X:JSR PRINT:LDA #<REVERSE:LDY #>REVERSE
23150 JSR STROUT:LDX TEMPB:INX:CPX #16:BNE COLLOOP
23160 LDA TEMPA:STA 646
23170 LDA #13:LDX #5:RETLOOP JSR PRINT:DEX:BNE RETLOOP
23180 JMP READY
23190 ;
23200 UPDATE TYA:CLC:ADC GETPTR:STA GETPTR:LDA GETPTR+1:ADC #0:STA GETPTR+1
23210 RTS
30100 ; W-64c FILE#3
30110 ;
30120 RENUM JSR CRUNCH:JSR CHRGET:LDA #10:STA RENINC
30130 LDA #100:STA RENSTART
30140 LDA #0:STA RENSTART+1
30150 JSR CHRGOT
30160 BEQ RENDEF
30170 JSR GETFXD
30180 STA RENSTART:STX RENSTART+1
30190 ;
30200 JSR COMMA
30210 JSR GETFXD:BEQ OKINC
30220 JMP ILLQTY
30230 OKINC STA RENINC
30240 ;
30250 JSR CHRGOT:BEQ RENDEF
30260 JSR COMMA
30270 ;
30280 RENDEF JSR LNRANGE
30290 ;
30300 LDA RENSTART:LDX RENSTART+1
30310 STA INTVAL:STX INTVAL+1
30320 JSR FINDLN
30330 LDY #3:LDA (VARPTR),Y:CMP STRPTR+1:BCC RANGEBAD:BNE YOUBET:DEY
30340 LDA (VARPTR),Y:CMP STRPTR:BCC RANGEBAD
30350 YOUBET LDA STRPTR:LDX STRPTR+1
30360 STA INTVAL:STX INTVAL+1
30370 JSR FINDLN
30380 LDA VARPTR:LDX VARPTR+1
30390 STA RENGET:STX RENGET+1
30400 ;
30410 LDA ENDPTR:LDX ENDPTR+1
30420 STA RENENDLN:STX RENENDLN+1
30430 STA INTVAL:STX INTVAL+1
30440 INC INTVAL:BNE STCOUNT
30450 INC INTVAL+1
30460 STCOUNT JSR COUNTEM
30470 ;
30480 JSR MULT
30490 LDA RENTEMP+1:CMP #$FA:BCC RANGEOK
30500 RANGEBAD LDA #<RENMSGE1:LDY #>RENMSGE1:JSR STROUT:JMP READY
30510 ;
30520 RANGEOK JSR FINDLN:JSR RGETPTR
30530 LDY #1:LDA (GETPTR),Y:BEQ THEMEAT
30540 LDY #3:LDA RENTEMP+1:CMP (GETPTR),Y:BCC THEMEAT
30550 BNE RANGEBAD
30560 LDA RENTEMP:DEY:CMP (GETPTR),Y:BCS RANGEBAD
30570 THEMEAT LDA STARTB:LDX STARTB+1
30580 STA GETPTR:STX GETPTR+1
30590 ;
30600 ; PASS1
30610 ;
30620 RESETQU LDY #0:STY QUMODE
30630 LINKLOPP LDY #1:LDA (GETPTR),Y:BNE LINKOK
30640 RENDONE1 JMP PASS2
30650 ;
30660 LINKOK INY:LDA (GETPTR),Y:STA INTVAL:INY:LDA (GETPTR),Y:STA INTVAL+1
30670 LDY #4:JSR UPDATE:JSR SGETPTR3:LDY #$FF
30680 ALOOP INY:ALOOP2 LDA (GETPTR),Y:BEQ RLNDONE
30690 CMP #34:BEQ QUFOUND
30700 LDX QUMODE:BNE ALOOP
30710 LDX #6:TABLELOP CMP RENTBLE-1,X:BEQ RENBINGO
30720 DEX:BNE TABLELOP
30730 BEQ ALOOP
30740 ;
30750 QUFOUND LDA QUMODE:EOR #$FF:STA QUMODE
30760 JMP ALOOP
30770 ;
30780 RLNDONE INY:JSR UPDATE:JMP RESETQU
30790 ;
30800 RENBINGO PHA:JSR UPDATE:LDY #0:PLA
30810 CMP #$CB:BNE RENBING2:JSR CHRGET
30820 CMP #$A4:BNE ALOOP2
30830 RENBING2 JSR CHRGET:BCS ALOOP2
30840 ;
30850 JSR SINTVAL:JSR SGETPTR2:JSR CHRGOT:JSR STRFAC:JSR FLFXD
30860 ;
30870 JSR CKRANGE:BEQ NEEDCHNG
30880 JMP AFTERNUM
30890 ;
30900 ;
30910 NEEDCHNG LDA INTVAL:LDX INTVAL+1
30920 STA RENENDLN:STX RENENDLN+1
30930 JSR COUNTEM:BEQ SOMELAB:LDA #$FF:STA RENCOUNT:STA RENCOUNT+1
30940 SOMELAB JSR SPACE
30950 ;
30960 JSR MULT
30970 LDX RENTEMP:LDA RENTEMP+1
30980 STX INTVAL:STA INTVAL+1
30990 JSR PRINTLN
31000 LDY RENTEMP:LDA RENTEMP+1:JSR FXDFL
31010 ;
31020 JSR RGETPTR3:LDX #$FF
31030 ;
31040 DOITLOOP LDY #0:INX
31050 LDA GETPTR+1:CMP TEMPC+1
31060 BCC NOPROB2:BNE DONETHIS
31070 LDA GETPTR:CMP TEMPC
31080 BCS DONETHIS
31090 NOPROB2 LDA (GETPTR),Y:STA BUFF,X:INY
31100 JSR UPDATE:JMP DOITLOOP
31110 DONETHIS LDY #0:LDA $0100:CMP #"-":BNE BIGLOOP:STA BUFF,X:INX
31120 BIGLOOP LDA $0101,Y:BEQ DONEPT2:STA BUFF,X:INY:INX:BNE BIGLOOP
31130 DONEPT2 STX TEMPH:JSR RGETPTR2:JSR CHRGOT:JSR STRFAC:JSR CHRGOT
31140 LDX TEMPH:LDY #0:BIG2LOOP CMP #0:STA BUFF,X:BEQ DONEPT3
31150 INX:INY:LDA (GETPTR),Y:JMP BIG2LOOP
31160 ;
31170 DONEPT3 INX:INX:STA BUFF,X:INX:INX:INX:STX $0B
31180 JSR RINTVAL:JSR PUTIN
31190 JSR RGETPTR2:JSR STRFAC
31200 LDA STRPTR:LDX STRPTR+1
31210 STA INTVAL:STX INTVAL+1
31220 JSR FINDLN:LDA VARPTR:LDX VARPTR+1
31230 STA RENGET:STX RENGET+1
31240 ;
31250 AFTERNUM JSR RINTVAL
31260 ;
31270 JSR CHRGOT:CMP #",":BEQ GETNEXT1
31280 CMP #$AB:BEQ GETNEXT1
31290 LDY #0:STY QUMODE:JMP ALOOP2
31300 ;
31310 GETNEXT1 JMP RENBING2
31320 ;
31330 ;
31340 ;
31350 GETFXD JSR INPFXD:GETINT LDA INTVAL
31360 LDX INTVAL+1:RTS
31370 ;
31380 ;
31390 MULT LDA RENINC:STA TEMPG:LDA RENSTART:LDX RENSTART+1
31400 STA RENTEMP:STX RENTEMP+1
31410 LDA #0:STA MULTFLAG
31420 ;
31430 LDX #8
31440 MULTLOOP LSR RENINC
31450 BCC DONTADD
31460 LDA MULTFLAG:BNE MULTERR
31470 LDA RENCOUNT
31480 CLC:ADC RENTEMP
31490 STA RENTEMP
31500 LDA RENCOUNT+1
31510 ADC RENTEMP+1
31520 STA RENTEMP+1
31530 ;
31540 DONTADD ASL RENCOUNT
31550 ROL RENCOUNT+1
31560 ROL MULTFLAG
31570 DEX
31580 BNE MULTLOOP
31590 ;
31600 LDA TEMPG:STA RENINC
31610 RTS
31620 MULTERR LDA #$FF
31630 STA RENTEMP:STA RENTEMP+1:LDA TEMPG:STA RENINC
31640 RTS
31650 ;
31660 PASS2 LDA RENGET:LDX RENGET+1
31670 ;
31680 STA GETPTR:STX GETPTR+1
31690 PASS2LP LDY #1:LDA (GETPTR),Y:BEQ PASS2DN
31700 LDY #3:LDA (GETPTR),Y
31710 CMP ENDPTR+1:BCC NOPROB:BNE PASS2DN
31720 DEY:LDA (GETPTR),Y
31730 CMP ENDPTR:BCC NOPROB:BNE PASS2DN
31740 ;
31750 NOPROB LDY #2:LDA RENSTART
31760 STA (GETPTR),Y
31770 INY:LDA RENSTART+1
31780 STA (GETPTR),Y
31790 ;
31800 LDY #0:LDA (GETPTR),Y:TAX
31810 INY:LDA (GETPTR),Y
31820 STA GETPTR+1:STX GETPTR
31830 LDA RENINC
31840 CLC:ADC RENSTART:STA RENSTART
31850 BCC PASS2LP
31860 LDA RENSTART+1:ADC #0:STA RENSTART+1
31870 JMP PASS2LP
31880 ;
31890 PASS2DN JMP READY
31900 ;
31910 ;
31920 SERROR9 JMP SERROR
31930 ;
31940 MEMORY LDA #$FF:STA ENDPTR:STA ENDPTR+1
31950 DEC GETPTR:LDA GETPTR:CMP #$FF:BNE NOSUBT
31960 DEC GETPTR+1
31970 NOSUBT JSR INPADD
31980 STA STRPTR:STX STRPTR+1
31990 JSR UPDATE:JSR CHRGET:BEQ GETGOING:CMP #",":BNE SERROR9
32000 JSR INPADD
32010 STA ENDPTR:STX ENDPTR+1
32020 ;
32030 GETGOING JSR CGRET
32040 LLOOPP JSR STOPSHFT:BEQ PASS2DN
32050 INC BLOCK
32060 LDA STRPTR+1:CMP BLOCK:BCC LETEM1
32070 LDA BLOCK:CLC:ADC #$10:STA TEMPH:LDY STRPTR+1:CPY TEMPH:BCS LETEM1
32080 ;
32090 DEC BLOCK
32100 JMP READY
32110 LETEM1 DEC BLOCK:LDA #".":JSR PRINT
32120 LDA STRPTR+1:LDX STRPTR
32130 JSR PRINTADD
32140 LDY #0
32150 PRINTMOR JSR SPACE
32160 LDA (STRPTR),Y
32170 STY TEMPH:JSR PRINTBYT:LDY TEMPH
32180 INY:CPY #8:BNE PRINTMOR
32190 JSR SPACE
32200 LDA #"{RVS ON}":JSR PRINT
32210 LDY #0
32220 BYTLOOP LDA (STRPTR),Y
32230 LDX #0:STX QUMODE
32240 CMP #161:BCS OOKK
32250 CMP #32:BCC NOTOOKK
32260 CMP #128:BCC OOKK
32270 NOTOOKK LDA #$2E
32280 OOKK JSR PRINT
32290 INY:CPY #8:BNE BYTLOOP
32300 ;
32310 TYA:CLC:ADC STRPTR:STA STRPTR
32320 LDA STRPTR+1:ADC #0:STA STRPTR+1
32330 JSR CGRET
32340 ;
32350 ;
32360 LDA STRPTR+1:CMP ENDPTR+1:BCC GOLLOOPP
32370 BNE NOLLOOPP
32380 ;
32390 LDA STRPTR:CMP ENDPTR:BCC GOLLOOPP
32400 BNE NOLLOOPP
32410 ;
32420 GOLLOOPP JMP LLOOPP
32430 NOLLOOPP JMP READY
32440 ;
32450 ;PUT THE LINE IN
32460 PUTIN JSR FINDLN    ; FIND BASIC LINE
32470 BCC L1
32480 LDY #1:LDA ($5F),Y
32490 STA $23:LDA $2D:STA $22
32500 LDA $60:STA $25:LDA $5F
32510 DEY:SBC ($5F),Y
32520 CLC:ADC $2D
32530 STA $2D:STA $24
32540 LDA $2E:ADC #$FF
32550 STA $2E:SBC $60
32560 TAX
32570 SEC:LDA $5F
32580 SBC $2D:TAY
32590 BCS L2
32600 INX:DEC $25
32610 L2 CLC:ADC $22
32620 BCC L3
32630 DEC $23:CLC
32640 L3 LDA ($22),Y
32650 STA ($24),Y
32660 INY:BNE L3
32670 INC $23:INC $25
32680 DEX:BNE L3
32690 L1 JSR BACKTEXT:JSR NEW ; UNDER [ NEW ]
32700 JSR RECHAIN
32710 LDA BUFF
32720 BEQ RETRN
32730 CLC:LDA $2D
32740 STA $5A:ADC $0B
32750 STA $58:LDY $2E
32760 STY $5B
32770 BCC L4
32780 INY
32790 L4 STY $59
32800 JSR MOVEMEM ; MOVE MEMORY
32810 LDA INTVAL:LDY INTVAL+1
32820 STA $01FE:STY $01FF
32830 LDA $31:LDY $32  ;END BASIC
32840 STA $2D:STY $2E  ;START BASIC VARS
32850 LDY $0B
32860 DEY
32870 L5 LDA $01FC,Y:STA ($5F),Y
32880 DEY:BPL L5
32890 JSR BACKTEXT:JSR NEW ; UNDER [ NEW ]
32900 JSR RECHAIN
32910 ;
32920 RETRN RTS
32930 ;
32940 NEW LDA $37:LDY $38
32950 STA $33:STY $34
32960 LDA $2D:LDY $2E
32970 STA $2F:STY $30
32980 STA $31:STY $32
32990 JSR $A81D
33000 LDX #$19:STX $16
33010 LDA #0:STA $3E:STA $10
33020 RTS
33030 ;
33040 COUNTEM LDA RENGET:LDX RENGET+1
33050 STA CNTPTR:STX CNTPTR+1
33060 LDY #0:STY RENCOUNT:STY RENCOUNT+1
33070 ;
33080 COUNTLP LDY #1:LDA (CNTPTR),Y
33090 BNE CNTNOVER:LDA #1:RTS
33100 ;
33110 CNTNOVER LDY #3:LDA (CNTPTR),Y
33120 CMP RENENDLN+1
33130 BCC CNTON
33140 BNE CNTDONE
33150 DEY:LDA (CNTPTR),Y
33160 CMP RENENDLN
33170 BCC CNTON
33180 ;
33190 CNTDONE RTS
33200 ;
33210 CNTON LDY #0
33220 LDA (CNTPTR),Y:TAX
33230 INY:LDA (CNTPTR),Y:STA CNTPTR+1:STX CNTPTR
33240 INC RENCOUNT:BNE COUNTLP
33250 INC RENCOUNT+1:BNE COUNTLP
33260 ;
33270 SINTVAL LDA INTVAL:LDX INTVAL+1
33280 STA TEMPA:STX TEMPB
33290 RTS
33300 ;
33310 RINTVAL LDA TEMPA:LDX TEMPB
33320 STA INTVAL:STX INTVAL+1
33330 RTS
33340 ;
33350 SGETPTR2 LDA GETPTR:LDX GETPTR+1
33360 STA TEMPC:STX TEMPC+1
33370 RTS
33380 ;
33390 RGETPTR2 LDA TEMPC:LDX TEMPC+1
33400 STA GETPTR:STX GETPTR+1
33410 RTS
33420 ;
33430 CKRANGE LDA INTVAL+1:CMP STRPTR+1
33440 BEQ ISEQU
33450 BCS CKENDPTR
33460 NOTRNGE LDA #1:RTS
33470 INRANGE LDA #0:RTS
33480 ;
33490 ISEQU LDA INTVAL:CMP STRPTR
33500 BCC NOTRNGE
33510 ;
33520 CKENDPTR LDA INTVAL+1:CMP ENDPTR+1
33530 BCC INRANGE
33540 BNE NOTRNGE
33550 LDA INTVAL:CMP ENDPTR
33560 BCC INRANGE
33570 BEQ INRANGE
33580 BCS NOTRNGE
33590 ;
33600 FXDFL CMP #$80:BCC SKP32768
33610 AND #$7F
33620 JSR SFXDFL:LDA #<CONSTANT:LDY #>CONSTANT
33630 JSR ADDEM
33640 JMP STRIT
33650 ;
33660 SKP32768 JSR SFXDFL
33670 STRIT JSR CONVFAC1
33680 RTS
33690 ;
33700 SGETPTR3 LDA GETPTR:LDX GETPTR+1
33710 STA TEMPI:STX TEMPI+1
33720 RTS
33730 ;
33740 RGETPTR3 LDA TEMPI:LDX TEMPI+1
33750 STA GETPTR:STX GETPTR+1
33760 RTS
33770 ;
33780 BONJOUR LDA #"?":STA DELIM:LDX #17:STX FILELEN
33790 JSR CHRGOT:JSR INPFILE
33800 JSR CGRET
33810 BONGET JSR GET:BEQ BONGET:CMP #",":BEQ LESAVE
33820 JMP READY
33830 ;
33840 LESAVE LDA 646:STA TEMPG:LDA 53281:STA 646
33850 LDY #0:LDA SAVETBLE+1:STA TEMPJ:STY SAVETBLE+1
33860 LDA SAVETBLE+2:STA TEMPJ+1:STY SAVETBLE+2
33870 JSR SAVEEE2
33880 LDA TEMPJ:STA SAVETBLE+1
33890 LDA TEMPJ+1:STA SAVETBLE+2
33900 LDA #147:JSR PRINT
33910 LDA TEMPG:STA 646
33920 JMP DS
33930 ;
33940 BLOCK .BYTE>BLOCKK-1
33950 ;
33960 RENTBLE .BYTE $8A,$9B,$CB,$A7,$8D,$89
33970 ;RUN,LIST,GO,THEN,GOSUB,GOTO
33980 RENMSGE1 .ASC " ** BAD LINE RANGE ** ":.BYTE 13,0
33990 CONSTANT .BYTE $90,0,0,0,0
34000 ;
34010 NAMESTR .ASC "        ":.BYTE 0
34020 ;
34030 TOKTBLE .ASC "ADJUST":.BYTE 0
34040 .ASC "AUTO":.BYTE 0
34050 .ASC "COLD":.BYTE 0
34060 .ASC "COLOUR":.BYTE 0
34070 .ASC "DEL":.BYTE 0
34080 .ASC "DS":.BYTE 0
34090 .ASC "HELP":.BYTE 0
34100 .ASC "HEX":.BYTE 0
34110 .ASC "HUNT":.BYTE 0
34120 .ASC "LOOK":.BYTE 0
34130 .ASC "MEM":.BYTE 0
34140 .ASC "MERGE":.BYTE 0
34150 .ASC "N":.BYTE 0
34160 .ASC "OFF":.BYTE 0
34170 .ASC "RENUM":.BYTE 0
34180 .ASC "SAVE":.BYTE 0
34190 .ASC "SEND":.BYTE 0
34200 .ASC "START".BYTE 0
34210 .ASC "$":.BYTE 0
34220 .ASC "/":.BYTE 0
34230 .ASC "{up arrow}HI":.BYTE 0
34240 .BYTE 0
34250 ;
34260 COMMANDS .BYTE 13:.ASC " COMMANDS :":.BYTE 13
34270 .ASC " ---------- ":.BYTE 13,0
34280 ;
34290 STMSGE .ASC "{DOWN}MERGING LINE: "
34300 .BYTE 0
34310 CRIGHT .ASC "{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}{RGHT}"
34320 .BYTE 0
34330 ;
34340 USERSTRG .ASC ",8:":.BYTE0
34350 ;
34360 REVERSE .ASC "{RVS ON}                    {RVS OFF}"
34370 .BYTE 13,0
34380 COLTBLE .ASC "{BLK}{WHT}{RED}{CYN}{pur}{GRN}{BLU}{YEL}{ORN}{BRW}{PNK}{DGRY}{GRY}{LGRN}{LBLU}{LGRY}"
34390 ;
34400 OURMSGE .ASC " ** INVALID WEDGE COMMAND ** ":.BYTE 13,0
34410 HELLO .BYTE 13:.ASC " WEDGE-64 (C)1983 D.MASON & A.WUNSCHE ":.BYTE 13
34420 .ASC " MERGE ADAPTED FROM BASIC AID ":.BYTE 13
34430 .ASC " PERMISSION TO USE BUT NOT TO SELL ":.BYTE 13,0
34440 OFFF .BYTE 13:.ASC " WEDGE-64 DISABLED ":.BYTE 13,0
34450 ;
34460 FILENAME *= *+55
34470 TOKNUM *=*+1
34480 TEMPX *=*+1
34490 TEMPY *=*+1
34500 TOKVCTR *=*+2
34510 FILELEN *=*+1
34520 TEMP8 *=*+1
34530 DELIM *=*+1
34540 STRLEN *= *+1
34550 TEMPA *= *+1:TEMPB *= *+1
34560 INCR *= *+1
34570 RENSTART *= *+2
34580 RENGET *= *+2
34590 TEMPC *= *+2
34600 RENENDLN *= *+2
34610 TEMPG *= *+1:TEMPH *= *+1
34620 TEMPI *= *+2:TEMPJ *= *+2
34630 ;
34640 ;