100 ;
110 ; W-64+ - FILE#2
120 ;
130 FINI JMP READY
140 DUMP LDA VARSTART:LDX VARSTART+1
150 STA VARPTR:STX VARPTR+1:JMP GETVAR
160 ;

390 NEXTVAR JSR STOPSHFT:BEQ FINI:LDA #13:JSR PRINT
400 LDA #7:CLC:ADC VARPTR:STA VARPTR
410 LDA VARPTR+1:ADC #0:STA VARPTR+1
420 ;
430 GETVAR LDA #"{SH SPACE}":JSR PRINT:LDA VARPTR+1
440 CMP STARRAY+1:BCC DUMPOK
450 BNE FINI
460 LDA VARPTR
470 CMP STARRAY:BCS FINI
480 ;
490 DUMPOK LDY #0:LDA (VARPTR),Y
500 BPL NOTINT
510 JMP INTEGER
520 NOTINT JSR PRINT
530 JSR INCPTR
540 CMP #$80
550 BCS STRING
560 FLOATING JSR PRINT:JSR EQUALS:LDA #"{LEFT}":JSR PRINT
570 JSR INCPTR
580 TYA:CLC:ADC VARPTR:STA TEMPX:LDA VARPTR+1:ADC #0:STA TEMPY
590 LDA TEMPX:LDY TEMPY:JSR LOADFAC1
600 JSR CONVFAC1
610 JSR STROUT
620 JMP NEXTVAR
630 ;
640 STRING AND #$7F:JSR PRINT:LDA #"$":JSR PRINT
650 JSR EQUALS:LDA #34:JSR PRINT
660 JSR INCPTR:STA STRLEN
670 JSR INCPTR:STA STRPTR
680 JSR INCPTR:STA STRPTR+1
690 LDY STRLEN:BEQ QUOTE
700 LDY #0:PRLOOP LDA (STRPTR),Y
710 JSR PRINT:INY:CPY STRLEN:BNE PRLOOP
720 QUOTE LDA #34:JSR PRINT
730 JMP NEXTVAR
740 ;
750 INTEGER AND #$7F:JSR PRINT
760 JSR INCPTR:AND #$7F:JSR PRINT
770 LDA #"%":JSR PRINT:JSR EQUALS
780 JSR INCPTR:STA TEMPA ; GET HI BYTE
790 ; HAS HIGH BIT SET IF NEG'VE
800 BPL PLUS
810 LDA #"-":JSR PRINT
820 LDA TEMPA:EOR #$FF:CLC:ADC #0
830 PHA:JSR INCPTR:EOR #$FF:TAX:INX:BNE PULL:PLA:CLC:ADC #1:JMP PRINTIT
840 PULL PLA:JMP PRINTIT
850 PLUS LDA TEMPA:AND #$7F
860 PHA:JSR INCPTR
870 TAX
880 PLA
890 PRINTIT JSR PRINTLN
900 JMP NEXTVAR
910 ;
920 INCPTR INY:LDA (VARPTR),Y
930 RTS
940 EQUALS LDA #" ":TAX:JSR PRINT:LDA #"=":JSR PRINT:TXA
950 JMP PRINT
960 ;
970 ;  H U N T
980 ;
990 HUNT JSR CRUNCH:JSR CHRGET:LDX #80:STX FILELEN
1000 STA DELIM:JSR INPFILE
1010 ;
1020 BEQ ONLYDL
1030 JSR COMMA
1040 ONLYDL JSR LNRANGE
1050 ;
1060 LDY STRPTR:LDX STRPTR+1:STY INTVAL:STX INTVAL+1
1070 LDA #4:CLC:ADC NUMCHAR:STA NUMCHAR
1080 JSR FINDLN
1090 ;
1100 NEWLINE JSR STOPSHFT:BEQ ALLGONE
1110 LDY #1:LDA (VARPTR),Y  ;HB LINK
1120 BEQ ALLGONE
1130 ;
1140 INY:LDA (VARPTR),Y
1150 STA INTVAL          ; GET LN#
1160 INY:LDA (VARPTR),Y
1170 STA INTVAL+1
1180 ;
1190 LDA INTVAL+1
1200 CMP ENDPTR+1
1210 BCC HUNTLOOP     ;CHECK HB
1220 BNE ALLGONE
1230 ;
1240 LDA INTVAL
1250 CMP ENDPTR
1260 BEQ HUNTLOOP     ;CHECK LB
1270 BCS ALLGONE
1280 ;
1290 HUNTLOOP LDY #4
1300 KEEPTRY LDA (VARPTR),Y
1310 BEQ NEXTLINE
1320 CMP FILENAME-4,Y
1330 BEQ OKSOFAR
1340 ;
1350 INC VARPTR
1360 BNE OKKKK
1370 INC VARPTR+1
1380 OKKKK JMP HUNTLOOP
1390 ;
1400 OKSOFAR INY   ;GOOD CHR->GET NEXT
1410 CPY NUMCHAR
1420 BNE KEEPTRY
1430 ;
1440 JSR FINDLN:JSR PRLINE   ;BINGO!
1450 ;
1460 ;
1470 NEXTLINE TYA:SEC
1480 ADC VARPTR        ;UPDATE PTR TO
1490 STA VARPTR
1500 BCC NEWLINE       ;NEXT LINE
1510 INC VARPTR+1
1520 JMP NEWLINE
1530 ;
1540 ALLGONE JMP READY
1550 ;
1560 PRLINE LDY #1:LDA #" ":JSR PRINT
1570 STY $0F
1580 LDA (VARPTR),Y
1590 BEQ LAB1
1600 JSR $AAD7
1610 INY:LDA (VARPTR),Y
1620 TAX
1630 INY:LDA (VARPTR),Y
1640 STY $49
1650 JSR PRINTLN
1660 LDA #" "
1670 LAB8 LDY $49
1680 AND #$7F
1690 LAB4 JSR $AB47
1700 CMP #$22
1710 BNE LAB2
1720 LDA $0F
1730 EOR #$FF
1740 STA $0F
1750 ;
1760 LAB2 INY
1770 BEQ LAB1
1780 LDA (VARPTR),Y
1790 BNE LAB3
1800 ;
1810 LAB1 RTS        ;ALL DONE!
1820 ;
1830 LAB3 BPL LAB4
1840 CMP #$FF
1850 BEQ LAB4
1860 BIT $0F
1870 BMI LAB4
1880 ;
1890 SEC:SBC #$7F
1900 TAX
1910 STY $49
1920 LDY #$FF
1930 LAB7 DEX
1940 BEQ LAB5
1950 ;
1960 LAB6 INY:LDA $A09E,Y
1970 BPL LAB6
1980 BMI LAB7
1990 ;
2000 LAB5 INY:LDA $A09E,Y
2010 BMI LAB8
2020 JSR $AB47
2030 BNE LAB5
2040 ;
2050 STOPSHFT JSR STOPKEY
2060 BEQ GOBACK
2070 SHIFTON LDA SHIFTKEY
2080 CMP #1
2090 BEQ SHIFTON
2100 GOBACK RTS
2110 ;
2120 ILLEGAL JMP ILLQTY
2130 ;
2140 COL BEQ READYYYY:JSR GETFXD:BNE ILLEGAL
2150 STA 53280
2160 JSR COMMA
2170 JSR GETFXD:BNE ILLEGAL
2180 STA 53281
2190 JSR COMMA
2200 JSR GETFXD:BNE ILLEGAL
2210 STA 646
2220 READYYYY JMP READY
2230 ;
2240 ;
2250 ;
2260 TOOBIG JMP ILLQTY
2270 AUTO JSR GETFXD
2280 BNE TOOBIG
2290 STA INCR:JMP READY
2300 ;
2310 ;
2320 AUTOLN JSR INPFXD:LDY INCR:BEQ GOBACK4
2330 JSR CHRGOT:BEQ GOBACK4
2340 LDA INCR
2350 CLC:ADC INTVAL:TAY:LDA INTVAL+1:ADC #0
2360 JSR FXDFL
2370 LDX #0:KEYBLOOP LDA $0101,X:BEQ KEYBDONE:STA KEYB,X:INX:BNE KEYBLOOP
2380 KEYBDONE INX
2390 LDA #" ":STA KEYB,X
2400 INX:STX KEYBLEN
2410 GOBACK4 JMP FOUNDNUM
2420 ;
2430 DEL BEQ READYY
2440 JSR CRUNCH:JSR CHRGET:JSR LNRANGE
2450 LDA STRPTR:LDX STRPTR+1
2460 STA INTVAL:STX INTVAL+1
2470 JSR FINDLN
2480 LDA VARPTR:LDX VARPTR+1
2490 STA MOVETO:STX MOVETO+1
2500 LDX ENDPTR:LDY ENDPTR+1
2510 INX:BNE STORE:INY:STORE STX INTVAL:STY INTVAL+1
2520 JSR FINDLN:LDA VARPTR:LDX VARPTR+1:STA MOVEFROM:STX MOVEFROM+1
2530 ;
2540 JSR DELMEM
2550 JSR RECHAIN
2560 LDA RECHNPTR:CLC:ADC #2:STA VARSTART
2570 LDA RECHNPTR+1:ADC #0:STA VARSTART+1
2580 JSR NEW
2590 READYY JMP READY
2600 ;
2610 ;
2620 ;
2630 LNRANGE LDY #$FF
2640 STY ENDPTR+1
2650 INY:STY ENDPTR:STY STRPTR:STY STRPTR+1
2660 JSR CHRGOT:BEQ GOBACK2
2670 BCC PAR1
2680 CMP #$AB:BNE LASTLBL
2690 JSR CHRGET:BCS BADPAR:JMP PAR2
2700 ;
2710 BADPAR JMP SYNERROR
2720 ;
2730 PAR1 JSR INPFXD
2740 LDA INTVAL:LDX INTVAL+1
2750 STA STRPTR:STX STRPTR+1
2760 JSR CHRGOT:CMP #$AB:BNE BADPAR
2770 JSR CHRGET:BEQ GOBACK2
2780 BCC PAR2
2790 GOBACK2 RTS
2795 LASTLBL JMP SYNERROR
2800 ;
2810 PAR2 JSR INPFXD
2820 LDA INTVAL:LDX INTVAL+1
2830 STA ENDPTR:STX ENDPTR+1
2840 JMP CHRGOT
2850 DELMEM LDY #0
2860 DELLOOP LDA MOVEFROM+1:CMP VARSTART+1
2870 BCC STARTDEL
2880 BNE DELDONE
2890 LDA MOVEFROM:CMP VARSTART
2900 BCS DELDONE
2910 ;
2920 STARTDEL LDA (MOVEFROM),Y
2930 STA (MOVETO),Y
2940 INC MOVEFROM:BNE XXX
2950 INC MOVEFROM+1
2960 XXX INC MOVETO:BNE YYY
2970 INC MOVETO+1
2980 YYY BNE DELLOOP
2990 ;
3000 ;
3010 DELDONE RTS
3020 ;
3030 ;
3040 SGETPTR LDA GETPTR:LDX GETPTR+1
3050 STA VARPTR:STX VARPTR+1
3060 RTS
3070 ;
3080 RGETPTR LDA VARPTR:LDX VARPTR+1
3090 STA GETPTR:STX GETPTR+1
3100 RTS
3110 ;
3120 SAVEM JSR SAVEEE:JMP DS
3130 ;
3140 SAVEEE LDX #17:STX FILELEN
3150 JSR INPFILE
3160 SAVEEE2 LDA #1:LDX #8:TAY:JSR SETLFS
3170 ;
3180 JSR CHRGOT:CMP #",":BEQ MLSAVE
3190 LDA #STARTB:LDX VARSTART
3200 LDY VARSTART+1
3210 SAVEIT JSR SAVEPROG:LDA #13:JMP PRINT
3220 ;
3240 ;
3250 MLSAVE JSR INPADD
3260 STA STRPTR:STX STRPTR+1
3270 JSR UPDATE:JSR CHRGET
3280 CMP #",":BNE MLSERR
3290 JSR INPADD
3300 STX TEMPX:TAX:JSR UPDATE:LDY TEMPX
3310 LDA #STRPTR
3320 JMP SAVEIT
3330 ;
3340 MLSERR JMP SYNERROR
3350 ;
3360 ADJUST LDX #0:LDA 646:STA TEMPA
3370 COLLOOP STX TEMPB:LDA COLTBLE,X:JSR PRINT:LDA #<REVERSE:LDY #>REVERSE
3380 JSR STROUT:LDX TEMPB:INX:CPX #16:BNE COLLOOP
3390 LDA TEMPA:STA 646
3400 LDA #13:LDX #5:RETLOOP JSR PRINT:DEX:BNE RETLOOP
3410 JMP READY
3420 ;
3430 REVERSE .ASC "{REVERSE ON}{SPACE*20}{REVERSE OFF}"
3440 .BYTE 13,0
3450 COLTBLE .ASC "{BLACK}{WHITE}{RED}{CYAN}{156}{GREEN}{BLUE}{YELLOW}{ORANGE}{BROWN}{PINK}{DARK GRAY}{GRAY}{LIGHT GREEN}{LIGHT BLUE}{LIGHT GRAY}"
3460 ;
3470 UPDATE TYA:CLC:ADC GETPTR:STA GETPTR:LDA GETPTR+1:ADC #0:STA GETPTR+1
3480 RTS
50000 .FILE 8,"W-64{POUND}.E6"