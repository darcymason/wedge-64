100 ;
110 ; W-64+ - FILE#2
120 ;
130 FINI JMP READY
140 DUMP LDA VARSTART:LDX VARSTART+1
150 STA VARPTR:STX VARPTR+1:JMP GETVAR
160 ;
170 NEXTVAR JSR STOPSHFT:BEQ FINI:JSR CGRET
180 LDA #7:CLC:ADC VARPTR:STA VARPTR
190 LDA VARPTR+1:ADC #0:STA VARPTR+1
200 ;
210 GETVAR JSR SPACE:LDA VARPTR+1
220 CMP STARRAY+1:BCC DUMPOK
230 BNE FINI
240 LDA VARPTR
250 CMP STARRAY:BCS FINI
260 ;
270 DUMPOK LDY #0:LDA (VARPTR),Y
280 BPL NOTINT
290 JMP INTEGER
300 NOTINT JSR PRINT
310 JSR INCPTR
320 CMP #$80
330 BCS STRING
340 FLOATING JSR PRINT:JSR EQUALS:LDA #"{LEFT}":JSR PRINT
350 JSR INCPTR
360 TYA:CLC:ADC VARPTR:STA TEMPX:LDA VARPTR+1:ADC #0:STA TEMPY
370 LDA TEMPX:LDY TEMPY:JSR LOADFAC1
380 JSR CONVFAC1
390 JSR STROUT
400 JMP NEXTVAR
410 ;
420 STRING AND #$7F:JSR PRINT:LDA #"$":JSR PRINT
430 JSR EQUALS:LDA #34:JSR PRINT
440 JSR INCPTR:STA STRLEN
450 JSR INCPTR:STA STRPTR
460 JSR INCPTR:STA STRPTR+1
470 LDY STRLEN:BEQ QUOTE
480 LDY #0:PRLOOP LDA (STRPTR),Y
490 JSR PRINT:INY:CPY STRLEN:BNE PRLOOP
500 QUOTE LDA #34:JSR PRINT
510 JMP NEXTVAR
520 ;
530 INTEGER AND #$7F:JSR PRINT
540 JSR INCPTR:AND #$7F:JSR PRINT
550 LDA #"%":JSR PRINT:JSR EQUALS
560 JSR INCPTR:STA TEMPA ; GET HI BYTE
570 ; HAS HIGH BIT SET IF NEG'VE
580 BPL PLUS


590 LDA #"-":JSR PRINT
600 LDA TEMPA:EOR #$FF:CLC:ADC #0
610 PHA:JSR INCPTR:EOR #$FF:TAX:INX:BNE PULL:PLA:CLC:ADC #1:JMP PRINTIT
620 PULL PLA:JMP PRINTIT
630 PLUS LDA TEMPA:AND #$7F
640 PHA:JSR INCPTR
650 TAX
660 PLA
670 PRINTIT JSR PRINTLN
680 JMP NEXTVAR
690 ;
700 INCPTR INY:LDA (VARPTR),Y
710 RTS
720 EQUALS LDA #" ":TAX:JSR PRINT:LDA #"=":JSR PRINT:TXA
730 JMP PRINT
740 ;
750 ;  H U N T
760 ;
770 HUNT JSR CRUNCH:JSR CHRGET:LDX #80:STX FILELEN
780 STA DELIM:JSR INPFILE
790 ;
800 BEQ ONLYDL
810 JSR COMMA
820 ONLYDL JSR LNRANGE
830 ;
840 LDY STRPTR:LDX STRPTR+1:STY INTVAL:STX INTVAL+1
850 LDA #4:CLC:ADC NUMCHAR:STA NUMCHAR
860 JSR FINDLN
870 ;
880 NEWLINE JSR STOPSHFT:BEQ ALLGONE
890 LDY #1:LDA (VARPTR),Y  ;HB LINK
900 BEQ ALLGONE
910 ;
920 INY:LDA (VARPTR),Y
930 STA INTVAL          ; GET LN#
940 INY:LDA (VARPTR),Y
950 STA INTVAL+1
960 ;
970 LDA INTVAL+1
980 CMP ENDPTR+1
990 BCC HUNTLOOP     ;CHECK HB
1000 BNE ALLGONE
1010 ;
1020 LDA INTVAL
1030 CMP ENDPTR
1040 BEQ HUNTLOOP     ;CHECK LB
1050 BCS ALLGONE
1060 ;
1070 HUNTLOOP LDY #4
1080 KEEPTRY LDA (VARPTR),Y
1090 BEQ NEXTLINE
1100 CMP FILENAME-4,Y
1110 BEQ OKSOFAR
1120 ;
1130 INC VARPTR
1140 BNE OKKKK
1150 INC VARPTR+1
1160 OKKKK JMP HUNTLOOP
1170 ;
1180 OKSOFAR INY   ;GOOD CHR->GET NEXT
1190 CPY NUMCHAR
1200 BNE KEEPTRY
1210 ;
1220 JSR FINDLN:JSR PRLINE   ;BINGO!
1230 ;
1240 ;
1250 NEXTLINE TYA:SEC
1260 ADC VARPTR        ;UPDATE PTR TO
1270 STA VARPTR
1280 BCC NEWLINE       ;NEXT LINE
1290 INC VARPTR+1
1300 JMP NEWLINE
1310 ;
1320 ALLGONE JMP READY
1330 ;
1340 PRLINE LDY #1:LDA #" ":JSR PRINT
1350 STY $0F
1360 LDA (VARPTR),Y
1370 BEQ LAB1
1380 JSR $AAD7
1390 INY:LDA (VARPTR),Y
1400 TAX
1410 INY:LDA (VARPTR),Y
1420 STY $49
1430 JSR PRINTLN
1440 LDA #" "
1450 LAB8 LDY $49
1460 AND #$7F
1470 LAB4 JSR $AB47
1480 CMP #$22
1490 BNE LAB2
1500 LDA $0F
1510 EOR #$FF
1520 STA $0F
1530 ;
1540 LAB2 INY
1550 BEQ LAB1
1560 LDA (VARPTR),Y
1570 BNE LAB3
1580 ;
1590 LAB1 RTS        ;ALL DONE!
1600 ;
1610 LAB3 BPL LAB4
1620 CMP #$FF
1630 BEQ LAB4
1640 BIT $0F
1650 BMI LAB4
1660 ;
1670 SEC:SBC #$7F
1680 TAX
1690 STY $49
1700 LDY #$FF
1710 LAB7 DEX
1720 BEQ LAB5
1730 ;
1740 LAB6 INY:LDA $A09E,Y
1750 BPL LAB6
1760 BMI LAB7
1770 ;
1780 LAB5 INY:LDA $A09E,Y
1790 BMI LAB8
1800 JSR $AB47
1810 BNE LAB5
1820 ;
1830 STOPSHFT JSR STOPKEY
1840 BEQ GOBACK
1850 SHIFTON LDA SHIFTKEY
1860 CMP #1
1870 BEQ SHIFTON
1880 GOBACK RTS
1890 ;
1900 ILLEGAL JMP ILLQTY
1910 ;
1920 COL BEQ READYYYY:JSR GETFXD:BNE ILLEGAL
1930 STA 53280
1940 JSR COMMA
1950 JSR GETFXD:BNE ILLEGAL
1960 STA 53281
1970 JSR COMMA
1980 JSR GETFXD:BNE ILLEGAL
1990 STA 646
2000 READYYYY JMP READY
2010 ;
2020 ;
2030 ;
2040 TOOBIG JMP ILLQTY
2050 AUTO JSR GETFXD
2060 BNE TOOBIG
2070 STA INCR:JMP READY
2080 ;
2090 ;
2100 AUTOLN JSR INPFXD:LDY INCR:BEQ GOBACK4
2110 JSR CHRGOT:BEQ GOBACK4
2120 LDA INCR
2130 CLC:ADC INTVAL:TAY:LDA INTVAL+1:ADC #0
2140 JSR FXDFL
2150 LDX #0:KEYBLOOP LDA $0101,X:BEQ KEYBDONE:STA KEYB,X:INX:BNE KEYBLOOP
2160 KEYBDONE INX
2170 LDA #" ":STA KEYB,X
2180 INX:STX KEYBLEN
2190 GOBACK4 JMP FOUNDNUM
2200 ;
2210 DEL BEQ READYY
2220 JSR CRUNCH:JSR CHRGET:JSR LNRANGE
2230 LDA STRPTR:LDX STRPTR+1
2240 STA INTVAL:STX INTVAL+1
2250 JSR FINDLN
2260 LDA VARPTR:LDX VARPTR+1
2270 STA MOVETO:STX MOVETO+1
2280 LDX ENDPTR:LDY ENDPTR+1
2290 INX:BNE STORE:INY:STORE STX INTVAL:STY INTVAL+1
2300 JSR FINDLN:LDA VARPTR:LDX VARPTR+1:STA MOVEFROM:STX MOVEFROM+1
2310 ;
2320 JSR DELMEM
2330 JSR RECHAIN
2340 LDA RECHNPTR:CLC:ADC #2:STA VARSTART
2350 LDA RECHNPTR+1:ADC #0:STA VARSTART+1
2360 JSR NEW
2370 READYY JMP READY
2380 ;
2390 ;
2400 ;
2410 LNRANGE LDY #$FF
2420 STY ENDPTR+1
2430 INY:STY ENDPTR:STY STRPTR:STY STRPTR+1
2440 JSR CHRGOT:BEQ GOBACK2
2450 BCC PAR1
2460 CMP #$AB:BNE LASTLBL
2470 JSR CHRGET:BCS BADPAR:JMP PAR2
2480 ;
2490 BADPAR JMP SYNERROR
2500 ;
2510 PAR1 JSR INPFXD
2520 LDA INTVAL:LDX INTVAL+1
2530 STA STRPTR:STX STRPTR+1
2540 JSR CHRGOT:CMP #$AB:BNE BADPAR
2550 JSR CHRGET:BEQ GOBACK2
2560 BCC PAR2
2570 GOBACK2 RTS
2580 LASTLBL JMP SYNERROR
2590 ;
2600 PAR2 JSR INPFXD
2610 LDA INTVAL:LDX INTVAL+1
2620 STA ENDPTR:STX ENDPTR+1
2630 JMP CHRGOT
2640 DELMEM LDY #0
2650 DELLOOP LDA MOVEFROM+1:CMP VARSTART+1
2660 BCC STARTDEL
2670 BNE DELDONE
2680 LDA MOVEFROM:CMP VARSTART
2690 BCS DELDONE
2700 ;
2710 STARTDEL LDA (MOVEFROM),Y
2720 STA (MOVETO),Y
2730 INC MOVEFROM:BNE XXX
2740 INC MOVEFROM+1
2750 XXX INC MOVETO:BNE YYY
2760 INC MOVETO+1
2770 YYY BNE DELLOOP
2780 ;
2790 ;
2800 DELDONE RTS
2810 ;
2820 ;
2830 SGETPTR LDA GETPTR:LDX GETPTR+1
2840 STA VARPTR:STX VARPTR+1
2850 RTS
2860 ;
2870 RGETPTR LDA VARPTR:LDX VARPTR+1
2880 STA GETPTR:STX GETPTR+1
2890 RTS
2900 ;
2910 SAVEM JSR SAVEEE:JMP DS
2920 ;
2930 SAVEEE LDX #17:STX FILELEN
2940 JSR INPFILE
2950 SAVEEE2 LDA #1:LDX #8:TAY:JSR SETLFS
2960 ;
2970 JSR CHRGOT:CMP #",":BEQ MLSAVE
2980 LDA #STARTB:LDX VARSTART
2990 LDY VARSTART+1
3000 SAVEIT JSR SAVEPROG:LDA #13:JMP PRINT
3010 ;
3020 ;
3030 MLSAVE JSR INPADD
3040 STA STRPTR:STX STRPTR+1
3050 JSR UPDATE:JSR CHRGET
3060 CMP #",":BNE MLSERR
3070 JSR INPADD
3080 STX TEMPX:TAX:JSR UPDATE:LDY TEMPX
3090 LDA #STRPTR
3100 JMP SAVEIT
3110 ;
3120 MLSERR JMP SYNERROR
3130 ;
3140 ADJUST LDX #0:LDA 646:STA TEMPA
3150 COLLOOP STX TEMPB:LDA COLTBLE,X:JSR PRINT:LDA #<REVERSE:LDY #>REVERSE
3160 JSR STROUT:LDX TEMPB:INX:CPX #16:BNE COLLOOP
3170 LDA TEMPA:STA 646
3180 LDA #13:LDX #5:RETLOOP JSR PRINT:DEX:BNE RETLOOP
3190 JMP READY
3200 ;
3210 REVERSE .ASC "{REVERSE ON}{SPACE*20}{REVERSE OFF}"
3220 .BYTE 13,0
3230 COLTBLE .ASC "{BLACK}{WHITE}{RED}{CYAN}{156}{GREEN}{BLUE}{YELLOW}{ORANGE}{BROWN}{PINK}{DARK GRAY}{GRAY}{LIGHT GREEN}{LIGHT BLUE}{LIGHT GRAY}"
3240 ;
3250 UPDATE TYA:CLC:ADC GETPTR:STA GETPTR:LDA GETPTR+1:ADC #0:STA GETPTR+1
3260 RTS
50000 .FILE 8,"W-64{POUND}.E6"